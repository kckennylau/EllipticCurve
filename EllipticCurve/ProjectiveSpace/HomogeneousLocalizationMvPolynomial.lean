/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import Mathlib.RingTheory.GradedAlgebra.HomogeneousLocalization
import Mathlib.RingTheory.MvPolynomial.Homogeneous

/-! # Homogeneous Localization of MvPolynomial

In this file we show that the homogeneous localization of `MvPolynomial Ïƒ R` away from `X i` is
isomorphic to `MvPolynomial {k // k â‰  i} R`.
-/

namespace HomogeneousLocalization

@[simp]
lemma val_fromZeroRingHom {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) (r : R) :
    (fromZeroRingHom ğ’œ S (algebraMap _ _ r)).val = algebraMap _ _ r :=
  rfl

instance {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹] [AddCommMonoid Î¹]
      (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) :
    Algebra R (HomogeneousLocalization ğ’œ S) where
  algebraMap := (fromZeroRingHom ğ’œ S).comp (algebraMap R (ğ’œ 0))
  commutes' r x := mul_comm ..
  smul_def' r x := by ext; rw [val_smul, val_mul, Algebra.smul_def]; rfl

instance {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹] [AddCommMonoid Î¹]
      (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) :
    IsScalarTower R (ğ’œ 0) (HomogeneousLocalization ğ’œ S) :=
  .of_algebraMap_eq' rfl

instance {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹] [AddCommMonoid Î¹]
      (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) :
    IsScalarTower R (ğ’œ 0) (Localization S) :=
  .of_algebraMap_eq' rfl

instance {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹] [AddCommMonoid Î¹]
      (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) :
    IsScalarTower R (HomogeneousLocalization ğ’œ S) (Localization S) :=
  .of_algebraMap_eq' rfl

instance {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹] [AddCommMonoid Î¹]
      (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) :
    IsScalarTower (ğ’œ 0) (HomogeneousLocalization ğ’œ S) (Localization S) :=
  .of_algebraMap_eq' rfl

@[simp] lemma algebraMap_eq' {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) :
    algebraMap R (HomogeneousLocalization ğ’œ S) = (fromZeroRingHom ğ’œ S).comp (algebraMap _ _) := rfl

theorem algebraMap_apply' {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) (f : R) :
    algebraMap R (HomogeneousLocalization ğ’œ S) f = mk âŸ¨0, algebraMap _ _ f, 1, one_mem _âŸ© := rfl

theorem val_sum {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] {ğ’œ : Î¹ â†’ Submodule R A}
      {x : Submonoid A} [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]
      {Ïƒ : Type*} {S : Finset Ïƒ} {f : Ïƒ â†’ HomogeneousLocalization ğ’œ x} :
    (âˆ‘ s âˆˆ S, f s).val = âˆ‘ s âˆˆ S, (f s).val :=
  map_sum (algebraMap (HomogeneousLocalization ğ’œ x) _) _ _

theorem val_prod {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] {ğ’œ : Î¹ â†’ Submodule R A}
      {x : Submonoid A} [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]
      {Ïƒ : Type*} {S : Finset Ïƒ} {f : Ïƒ â†’ HomogeneousLocalization ğ’œ x} :
    (âˆ s âˆˆ S, f s).val = âˆ s âˆˆ S, (f s).val :=
  map_prod (algebraMap (HomogeneousLocalization ğ’œ x) _) _ _

namespace Away

theorem mk_smul {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] {f d hf n x} (hx) {r : R} :
    r â€¢ Away.mk ğ’œ (f:=f) hf (d:=d) n x hx = .mk ğ’œ hf n (r â€¢ x) (Submodule.smul_mem _ _ hx) := rfl

theorem algebraMap_apply {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (f : A) (d hf) (r : R) :
    algebraMap R (Away ğ’œ f) r = .mk ğ’œ (d:=d) hf 0 (algebraMap R A r)
      (by rw [zero_nsmul]; exact SetLike.algebraMap_mem_graded ..) := by
  ext; simp [fromZeroRingHom]

/-- If `f = g`, then `Away ğ’œ f â‰… Away ğ’œ g`. -/
@[simps! apply] noncomputable
def congr {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (f g : A)
      {d : Î¹} (hf : f âˆˆ ğ’œ d) (h : f = g) :
    Away ğ’œ f â‰ƒâ‚[R] Away ğ’œ g := by
  refine .ofRingEquiv (f := .ofRingHom (awayMap ğ’œ (SetLike.one_mem_graded ..) (by simp [h]))
    (awayMap ğ’œ (SetLike.one_mem_graded ..) (by simp [h]))
    (RingHom.ext fun x â†¦ ?_) (RingHom.ext fun x â†¦ ?_)) (fun x â†¦ ?_)
  Â· subst h; rcases Away.mk_surjective ğ’œ hf x with âŸ¨n, a, ha, rflâŸ©; simp
  Â· subst h; rcases Away.mk_surjective ğ’œ hf x with âŸ¨n, a, ha, rflâŸ©; simp
  Â· subst h; ext; simp [awayMap_fromZeroRingHom]

lemma congr_mk {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (f g : A)
      {d : Î¹} (hf : f âˆˆ ğ’œ d) (h : f = g) (n x hx) :
    congr ğ’œ f g hf h (.mk ğ’œ hf n x hx) = .mk ğ’œ (by rwa [â† h]) n x hx := by
  simp_rw [congr_apply, awayMap_mk, one_pow, mul_one, add_zero]

@[simp] lemma congr_symm {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (f g : A)
      {d : Î¹} (hf : f âˆˆ ğ’œ d) (h : f = g) :
    (congr ğ’œ f g hf h).symm = congr ğ’œ g f (by rwa [â† h]) h.symm :=
  rfl

end Away

end HomogeneousLocalization


namespace MvPolynomial

open Classical in
/-- Dehomogenisation of a polynomial, e.g. `XÂ²+2XY+3YÂ² â†¦ XÂ²+2X+3`. The variable to be removed
is specified. -/
noncomputable def dehomogenise {Ïƒ R : Type*} [CommSemiring R] (i : Ïƒ) :
    MvPolynomial Ïƒ R â†’â‚[R] MvPolynomial { k // k â‰  i } R :=
  aeval fun j â†¦ if H : j = i then 1 else X âŸ¨j, HâŸ©

theorem dehomogenise_C {Ïƒ R : Type*} [CommSemiring R] (i : Ïƒ) (r : R) :
    dehomogenise i (C r) = C r :=
  aeval_C ..

theorem dehomogenise_X_self {Ïƒ R : Type*} [CommSemiring R] (i : Ïƒ) :
    dehomogenise (R:=R) i (X i) = 1 := by
  rw [dehomogenise, aeval_X, dif_pos rfl]

@[simp] theorem dehomogenise_X_of_ne {Ïƒ R : Type*} [CommSemiring R] {i j : Ïƒ} (h : j â‰  i) :
    dehomogenise (R:=R) i (X j) = X âŸ¨j, hâŸ© := by
  rw [dehomogenise, aeval_X, dif_neg]

@[simp] theorem dehomogenise_X {Ïƒ R : Type*} [CommSemiring R] {i : Ïƒ} (j : {k // k â‰  i}) :
    dehomogenise (R:=R) i (X j) = X j := by
  simp [j.2]

@[simp] theorem dehomogenise_of_mem_X_powers {Ïƒ R : Type*} [CommSemiring R] {i : Ïƒ} {d}
    (hd : d âˆˆ Submonoid.powers (X (R := R) i)) : dehomogenise (R:=R) i d = 1 := by
  rcases hd with âŸ¨_, _, rflâŸ©; rw [map_pow, dehomogenise_X_self, one_pow]

theorem dehomogenise_X_powers {Ïƒ R : Type*} [CommSemiring R] (i : Ïƒ)
    (d : Submonoid.powers (X (R := R) i)) : dehomogenise (R:=R) i d = 1 :=
  dehomogenise_of_mem_X_powers d.2

/-- Functoriality. -/
theorem map_comp_dehomogenise {Ïƒ R S : Type*} [CommSemiring R] [CommSemiring S]
      (f : R â†’+* S) (i : Ïƒ) :
    (map f).comp (RingHomClass.toRingHom (dehomogenise (R:=R) i)) =
      (RingHomClass.toRingHom (dehomogenise (R:=S) i)).comp (map f) :=
  ringHom_ext (by simp) fun k â†¦ by by_cases h : k = i <;> simp [h]

/-- Functoriality. -/
theorem map_dehomogenise {Ïƒ R S : Type*} [CommSemiring R] [CommSemiring S]
      (f : R â†’+* S) (i : Ïƒ) (p : MvPolynomial Ïƒ R) :
    map f (dehomogenise i p) = dehomogenise i (map f p) :=
  RingHom.congr_fun (map_comp_dehomogenise f i) p

attribute [local instance] gradedAlgebra

open HomogeneousLocalization

variable {Ïƒ R : Type*} [CommRing R] (i : Ïƒ)

/-- Map `Xâ±¼/Xáµ¢` to `Xâ±¼`, contracting away the variable `Xáµ¢`. -/
noncomputable def contract {Ïƒ : Type*} (R : Type*) [CommRing R] (i : Ïƒ) :
    Away (homogeneousSubmodule Ïƒ R) (X i) â†’â‚[R] MvPolynomial { k // k â‰  i } R where
  toFun p := Quotient.liftOn p (fun q â†¦ q.num.val.dehomogenise i) fun qâ‚ qâ‚‚ hq â†¦
    let âŸ¨x, hxâŸ© := Localization.r_iff_exists.1 (Localization.mk_eq_mk_iff.1 hq)
    have := congr_arg (dehomogenise i) hx
    by simpa only [ne_eq, map_mul, SetLike.coe_mem, dehomogenise_of_mem_X_powers, qâ‚‚.den_mem,
      one_mul, qâ‚.den_mem] using this
  map_one' := map_one _
  map_mul' pâ‚ pâ‚‚ := Quotient.inductionOnâ‚‚ pâ‚ pâ‚‚ fun qâ‚ qâ‚‚ â†¦ map_mul ..
  map_zero' := map_zero _
  map_add' pâ‚ pâ‚‚ := Quotient.inductionOnâ‚‚ pâ‚ pâ‚‚ fun qâ‚ qâ‚‚ â†¦ show dehomogenise _ (_ + _) = _ by
    rw [map_add, map_mul, map_mul, dehomogenise_of_mem_X_powers qâ‚.den_mem,
      dehomogenise_of_mem_X_powers qâ‚‚.den_mem, one_mul, one_mul, add_comm]; rfl
  commutes' r := algHom_C ..

@[simp] theorem contract_mk {Ïƒ : Type*} (R : Type*) [CommRing R] (i : Ïƒ) (hx) (n : â„•) (f)
    (hf : f.IsHomogeneous _) :
  contract R i (.mk _ (d:=1) hx n f hf) = f.dehomogenise i := rfl

@[simp] theorem contract_mk' {Ïƒ : Type*} (R : Type*) [CommRing R] (i : Ïƒ) (q) :
  contract R i (mk q) = q.num.val.dehomogenise i := rfl

/-- Map `Xâ±¼` to `Xâ±¼/Xáµ¢`, expanding to the variable `Xáµ¢`. -/
noncomputable def expand {Ïƒ : Type*} (R : Type*) [CommRing R] (i : Ïƒ) :
    MvPolynomial { k // k â‰  i } R â†’â‚[R] Away (homogeneousSubmodule Ïƒ R) (X i) :=
  aeval fun j â†¦ .mk _ (isHomogeneous_X ..) 1 (X j) (isHomogeneous_X ..)

@[simp] theorem expand_C {Ïƒ R : Type*} [CommRing R] (i : Ïƒ) (r : R) :
    expand R i (C r) = .mk _ (isHomogeneous_X ..) 0 (C r) (isHomogeneous_C ..) :=
  algHom_C ..

@[simp] theorem expand_X {Ïƒ R : Type*} [CommRing R] (i : Ïƒ) (j) :
    expand R i (X j) = .mk _ (isHomogeneous_X ..) 1 (X j) (isHomogeneous_X ..) :=
  aeval_X ..

theorem expand_dehomogenise_monomial_one {Ïƒ R : Type*} [CommRing R] (i : Ïƒ) {d : â„•} {c : Ïƒ â†’â‚€ â„•}
    (hc : c.degree = d â€¢ 1) :
    expand R i ((monomial c 1).dehomogenise i) =
      .mk _ (isHomogeneous_X ..) d (monomial c 1) (isHomogeneous_monomial _ hc) := by
  ext : 1
  rw [Away.val_mk]
  rw [nsmul_one, Nat.cast_id] at hc
  cases hc; induction c using Finsupp.induction with
  | zero =>
      rw [monomial_zero', C_1, map_one, map_one, val_one, â† Localization.mk_one,
        Localization.mk_eq_mk_iff, Localization.r_iff_exists]
      exact âŸ¨1, by simpâŸ©
  | single_add c n b hc hn ih =>
      classical
      rw [monomial_single_add, map_mul, map_mul, val_mul, ih,
        map_pow, map_pow]
      by_cases hci : c = i
      Â· rw [hci, dehomogenise_X_self, map_one, one_pow, val_one, one_mul,
          Localization.mk_eq_mk_iff, Localization.r_iff_exists]
        exact âŸ¨1, by simp; ringâŸ©
      Â· rw [dehomogenise_X_of_ne hci, expand_X, val_pow, Away.val_mk,
          Localization.mk_pow, Localization.mk_mul,
          Localization.mk_eq_mk_iff, Localization.r_iff_exists]
        exact âŸ¨1, by simp [add_comm]; ringâŸ©

theorem expand_dehomogenise_monomial {Ïƒ R : Type*} [CommRing R] (i : Ïƒ) {d : â„•} {c : Ïƒ â†’â‚€ â„•}
      (hc : c.degree = d â€¢ 1) (r : R) :
    expand R i ((monomial c r).dehomogenise i) =
      .mk _ (isHomogeneous_X ..) d (monomial c r) (isHomogeneous_monomial _ hc) := by
  have : monomial c r = r â€¢ monomial c 1 := by rw [smul_monomial, smul_eq_mul, mul_one]
  conv_lhs => rw [this, map_smul, map_smul, expand_dehomogenise_monomial_one _ hc, Away.mk_smul]
  congr 1; exact this.symm

@[simp] theorem expand_dehomogenise_of_homogeneous {Ïƒ R : Type*} [CommRing R] (i : Ïƒ) {n : â„•}
      {p : MvPolynomial Ïƒ R} (hp : p.IsHomogeneous n) :
    expand R i (p.dehomogenise i) =
      .mk _ (isHomogeneous_X ..) n p (by rwa [nsmul_one]) := by
  ext
  rw [Away.val_mk, â† support_sum_monomial_coeff p, map_sum, map_sum, Localization.mk_sum, val_sum]
  congr 1; ext s; rw [expand_dehomogenise_monomial _ (by rw [nsmul_one, Nat.cast_id]), Away.val_mk]
  by_cases hs : s.degree = n
  Â· rw [hs]
  Â· rw [hp.coeff_eq_zero hs, monomial_zero, Localization.mk_zero, Localization.mk_zero]

@[simps!] noncomputable def algEquivAway (i : Ïƒ) :
    Away (homogeneousSubmodule Ïƒ R) (X i) â‰ƒâ‚[R] MvPolynomial { k // k â‰  i } R where
  __ := contract R i
  invFun := expand R i
  left_inv p := by
    change expand R i (contract R i p) = p
    rcases Away.mk_surjective _ (isHomogeneous_X ..) p with âŸ¨d, r, hr, rflâŸ©
    rw [contract_mk, expand_dehomogenise_of_homogeneous _ (by rwa [nsmul_one, Nat.cast_id] at hr)]
  right_inv p := by
    change contract R i (aeval _ p) = p
    induction p using induction_on
    Â· rw [aeval_C, algebraMap_apply', contract_mk',
        SetLike.GradeZero.coe_algebraMap, algebraMap_eq, dehomogenise_C]
    Â· simp only [map_add, *]
    Â· simp only [map_mul, *, aeval_X, contract_mk, dehomogenise_X]

end MvPolynomial
