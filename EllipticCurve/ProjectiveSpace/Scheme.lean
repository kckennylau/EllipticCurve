/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import EllipticCurve.ProjectiveSpace.Graded.AlgEquiv
import EllipticCurve.ProjectiveSpace.TensorProduct.Proj
import Mathlib.AlgebraicGeometry.Limits
import Mathlib.RingTheory.MvPolynomial.Homogeneous

/-!
# Projective Space as Scheme

## Main definitions

- `AlgebraicGeometry.ProjectiveSpace`: `â„™(n; S)` is the projective `n`-space over a scheme `S`.

## TODO

- `AlgebraicGeometry.ProjectiveSpace.SpecIso`: `â„™(n; Spec R) â‰… Proj R[n]`.
- `AlgebraicGeometry.ProjectiveSpace.openCover`: the canonical cover of `â„™(n; S)` by `n` affine
  charts. The `i`áµ—Ê° chart is `ð”¸({k // k â‰  i}; S) âŸ¶ â„™(n; S)`, and represents the open set where
  the function `Xáµ¢` does not vanish.
- Functoriality of `ProjectiveSpace` on the `S` component.
- Show that a map `Spec A âŸ¶ â„™(n; S)` corresponds to a map `p : Spec A âŸ¶ S` and a unique
  `A`-submodule `N` of `n â†’â‚€ A` such that `(n â†’â‚€ A) â§¸ N` is locally free of rank 1.
-/

-- Also see https://github.com/leanprover-community/mathlib4/pull/26061

universe u

open CategoryTheory Limits MvPolynomial HomogeneousLocalization

noncomputable section

namespace AlgebraicGeometry

open SpecOfNotation

variable (n : Type u) (S : Scheme.{u})

attribute [local instance] gradedAlgebra

/-- The structure of a graded algebra on `â„¤[n]`, i.e. on `MvPolynomial n (ULift.{u} â„¤)`. -/
local notation3 "â„¤[" n "].{" u "}" => homogeneousSubmodule n (ULift.{u} â„¤)

/-- `â„™(n; S)` is the projective `n`-space over `S`.
Note that `n` is an arbitrary index type (e.g. `ULift (Fin m)`). -/
def ProjectiveSpace (n : Type u) (S : Scheme.{u}) : Scheme.{u} :=
  pullback (terminal.from S) (terminal.from (Proj â„¤[n].{u}))

@[inherit_doc] scoped notation "â„™("n"; "S")" => ProjectiveSpace n S

instance _root_.ULift.algebraLeft {R A : Type*} [CommSemiring R] [Semiring A] [Algebra R A] :
    Algebra (ULift R) A where
  algebraMap := (algebraMap R A).comp (ULift.ringEquiv (R := R))
  commutes' _ := Algebra.commutes _
  smul_def' _ := Algebra.smul_def _

@[simps] def _root_.CategoryTheory.Limits.pullback.mapIso {C : Type*} [Category C]
    {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ Wâ‚ Wâ‚‚ : C} {fâ‚ : Xâ‚ âŸ¶ Wâ‚} {gâ‚ : Yâ‚ âŸ¶ Wâ‚} {fâ‚‚ : Xâ‚‚ âŸ¶ Wâ‚‚} {gâ‚‚ : Yâ‚‚ âŸ¶ Wâ‚‚}
    [HasPullback fâ‚ gâ‚] [HasPullback fâ‚‚ gâ‚‚] (eX : Xâ‚ â‰… Xâ‚‚) (eY : Yâ‚ â‰… Yâ‚‚) (eW : Wâ‚ â‰… Wâ‚‚)
    (commâ‚ : fâ‚ â‰« eW.hom = eX.hom â‰« fâ‚‚) (commâ‚‚ : gâ‚ â‰« eW.hom = eY.hom â‰« gâ‚‚) :
    pullback fâ‚ gâ‚ â‰… pullback fâ‚‚ gâ‚‚ where
  hom := pullback.map _ _ _ _ eX.hom eY.hom eW.hom commâ‚ commâ‚‚
  inv := pullback.map _ _ _ _ eX.inv eY.inv eW.inv
    (by rw [Iso.comp_inv_eq, Category.assoc, Iso.eq_inv_comp, commâ‚])
    (by rw [Iso.comp_inv_eq, Category.assoc, Iso.eq_inv_comp, commâ‚‚])
  hom_inv_id := pullback.hom_ext (by simp) (by simp)
  inv_hom_id := pullback.hom_ext (by simp) (by simp)

def _root_.MvPolynomial.algebraTensorGAlgEquiv (R Ïƒ A : Type*)
    [CommRing R] [CommRing A] [Algebra R A] :
    (homogeneousSubmodule Ïƒ R).baseChange A â‰ƒâ‚áµ[A] homogeneousSubmodule Ïƒ A where
  __ := MvPolynomial.algebraTensorAlgEquiv R A
  map_mem' {n x} hx := by
    obtain âŸ¨x, hxâŸ© := Submodule.toBaseChange_surjective _ _ âŸ¨x, hxâŸ©
    replace hx := congr(($hx).val); subst hx
    simp only [AlgEquiv.toEquiv_eq_coe, Equiv.toFun_as_coe, EquivLike.coe_coe,
      mem_homogeneousSubmodule]
    induction x with
    | zero => simp [isHomogeneous_zero]
    | add => simp [IsHomogeneous.add, *]
    | tmul a x => simpa [smul_eq_C_mul] using (x.2.map _).C_mul _

/-- `â„™(n; Spec(R))` is isomorphic to `Proj R[n]`. -/
def SpecIso (R : Type u) [CommRing R] : â„™(n; Spec(R)) â‰… Proj (homogeneousSubmodule n R) :=
  pullback.mapIso (Iso.refl _) (Iso.refl _) (terminalIsoIsTerminal specULiftZIsTerminal)
    (specULiftZIsTerminal.hom_ext ..) (specULiftZIsTerminal.hom_ext ..) â‰ªâ‰«
  (projTensorProduct â„¤[n].{u} R).symm â‰ªâ‰«
  Proj.congr (MvPolynomial.algebraTensorGAlgEquiv (ULift â„¤) n R)

end AlgebraicGeometry
