/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import Mathlib.RingTheory.GradedAlgebra.HomogeneousLocalization

/-! # Algebra Structure on Homogeneous Localization

In this file we show that if `A` is a graded `R`-algebra then `HomogeneousLocalization ğ’œ S` is
an `R`-algebra.
-/

variable {Î¹ Râ‚€ R A : Type*}

section Semiring

variable [CommSemiring Râ‚€] [CommSemiring R] [Semiring A]
  [Algebra Râ‚€ R] [Algebra R A] [Algebra Râ‚€ A] [IsScalarTower Râ‚€ R A]
  [DecidableEq Î¹] [AddMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]

instance : Algebra Râ‚€ (ğ’œ 0) where
  algebraMap := (algebraMap R (ğ’œ 0)).comp (algebraMap Râ‚€ R)
  commutes' r x := Algebra.commutes' (algebraMap Râ‚€ R r) x
  smul_def' r x := Subtype.ext <| by
    rw [â† IsScalarTower.algebraMap_smul R, Algebra.smul_def]
    rfl

@[simp] lemma algebraMap_to_zero (r : Râ‚€) : (algebraMap Râ‚€ (ğ’œ 0) r : A) = algebraMap Râ‚€ A r :=
  (IsScalarTower.algebraMap_apply Râ‚€ R A r).symm

end Semiring

section Ring

namespace HomogeneousLocalization

variable [CommRing Râ‚€] [CommRing R] [CommRing A]
  [Algebra Râ‚€ R] [Algebra R A] [Algebra Râ‚€ A] [IsScalarTower Râ‚€ R A]
  [DecidableEq Î¹] [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
  (S : Submonoid A)

@[simp]
lemma val_fromZeroRingHom (r : R) :
    (fromZeroRingHom ğ’œ S (algebraMap _ _ r)).val = algebraMap _ _ r :=
  rfl

instance : Algebra Râ‚€ (HomogeneousLocalization ğ’œ S) where
  algebraMap := (fromZeroRingHom ğ’œ S).comp (algebraMap Râ‚€ (ğ’œ 0))
  commutes' r x := mul_comm ..
  smul_def' r x := by ext; rw [val_smul, val_mul, Algebra.smul_def, RingHom.comp_apply,
    IsScalarTower.algebraMap_apply Râ‚€ R (ğ’œ 0), val_fromZeroRingHom,
    â† IsScalarTower.algebraMap_apply]

instance : IsScalarTower Râ‚€ (ğ’œ 0) (HomogeneousLocalization ğ’œ S) :=
  .of_algebraMap_eq' rfl

instance : IsScalarTower Râ‚€ R (HomogeneousLocalization ğ’œ S) :=
  .of_algebraMap_eq' rfl

instance : IsScalarTower R (ğ’œ 0) (Localization S) :=
  .of_algebraMap_eq' rfl

instance {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹] [AddCommMonoid Î¹]
      (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) :
    IsScalarTower R (HomogeneousLocalization ğ’œ S) (Localization S) :=
  .of_algebraMap_eq' rfl

instance {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹] [AddCommMonoid Î¹]
      (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) :
    IsScalarTower (ğ’œ 0) (HomogeneousLocalization ğ’œ S) (Localization S) :=
  .of_algebraMap_eq' rfl

@[simp] lemma algebraMap_eq' :
    algebraMap Râ‚€ (HomogeneousLocalization ğ’œ S) = (fromZeroRingHom ğ’œ S).comp (algebraMap _ _) := rfl

theorem algebraMap_apply' {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (S : Submonoid A) (f : R) :
    algebraMap R (HomogeneousLocalization ğ’œ S) f = mk âŸ¨0, algebraMap _ _ f, 1, one_mem _âŸ© := rfl

theorem val_sum {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] {ğ’œ : Î¹ â†’ Submodule R A}
      {x : Submonoid A} [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]
      {Ïƒ : Type*} {S : Finset Ïƒ} {f : Ïƒ â†’ HomogeneousLocalization ğ’œ x} :
    (âˆ‘ s âˆˆ S, f s).val = âˆ‘ s âˆˆ S, (f s).val :=
  map_sum (algebraMap (HomogeneousLocalization ğ’œ x) _) _ _

theorem val_prod {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] {ğ’œ : Î¹ â†’ Submodule R A}
      {x : Submonoid A} [AddCommMonoid Î¹] [DecidableEq Î¹] [GradedAlgebra ğ’œ]
      {Ïƒ : Type*} {S : Finset Ïƒ} {f : Ïƒ â†’ HomogeneousLocalization ğ’œ x} :
    (âˆ s âˆˆ S, f s).val = âˆ s âˆˆ S, (f s).val :=
  map_prod (algebraMap (HomogeneousLocalization ğ’œ x) _) _ _

namespace Away

theorem mk_smul {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] {f d hf n x} (hx) {r : R} :
    r â€¢ Away.mk ğ’œ (f:=f) hf (d:=d) n x hx = .mk ğ’œ hf n (r â€¢ x) (Submodule.smul_mem _ _ hx) := rfl

theorem algebraMap_apply {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (f : A) (d hf) (r : R) :
    algebraMap R (Away ğ’œ f) r = .mk ğ’œ (d:=d) hf 0 (algebraMap R A r)
      (by rw [zero_nsmul]; exact SetLike.algebraMap_mem_graded ..) := by
  ext; simp [fromZeroRingHom]

/-- If `f = g`, then `Away ğ’œ f â‰… Away ğ’œ g`. -/
@[simps! apply] noncomputable
def congr {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (f g : A)
      {d : Î¹} (hf : f âˆˆ ğ’œ d) (h : f = g) :
    Away ğ’œ f â‰ƒâ‚[R] Away ğ’œ g := by
  refine .ofRingEquiv (f := .ofRingHom (awayMap ğ’œ (SetLike.one_mem_graded ..) (by simp [h]))
    (awayMap ğ’œ (SetLike.one_mem_graded ..) (by simp [h]))
    (RingHom.ext fun x â†¦ ?_) (RingHom.ext fun x â†¦ ?_)) (fun x â†¦ ?_)
  Â· subst h; rcases Away.mk_surjective ğ’œ hf x with âŸ¨n, a, ha, rflâŸ©; simp
  Â· subst h; rcases Away.mk_surjective ğ’œ hf x with âŸ¨n, a, ha, rflâŸ©; simp
  Â· subst h; ext; simp [awayMap_fromZeroRingHom]

lemma congr_mk {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (f g : A)
      {d : Î¹} (hf : f âˆˆ ğ’œ d) (h : f = g) (n x hx) :
    congr ğ’œ f g hf h (.mk ğ’œ hf n x hx) = .mk ğ’œ (by rwa [â† h]) n x hx := by
  simp_rw [congr_apply, awayMap_mk, one_pow, mul_one, add_zero]

@[simp] lemma congr_symm {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A] [DecidableEq Î¹]
      [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ] (f g : A)
      {d : Î¹} (hf : f âˆˆ ğ’œ d) (h : f = g) :
    (congr ğ’œ f g hf h).symm = congr ğ’œ g f (by rwa [â† h]) h.symm :=
  rfl

end Away

end HomogeneousLocalization

end Ring
