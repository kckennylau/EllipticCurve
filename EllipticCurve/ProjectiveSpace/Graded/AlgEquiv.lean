/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import EllipticCurve.ProjectiveSpace.Graded.AlgHom
import EllipticCurve.ProjectiveSpace.Graded.RingEquiv

/-! # Graded ring isomorphisms
We define `GradedAlgEquiv ­Юњю Рёг` to mean isomorphisms of graded `R`-algebras, with notation
`­Юњю РЅЃРѓљрхЇ[R] Рёг`.

When possible, instead of parametrizing results over `(e : ­Юњю РЅЃРѓљрхЇ[R] Рёг)`, you should parametrize
over `[GradedEquivLike E ­Юњю Рёг] [AlgEquivClass E R A B] (e : E)`.
-/

variable {R A B C D ╬╣ S T U V : Type*}

/-- A graded `R`-algebra isomorphism between `­Юњю` and `Рёг`. -/
structure GradedAlgEquiv (R : Type*) {A B ╬╣ S T : Type*} [Semiring A] [Semiring B]
    [CommSemiring S] [Algebra S A] [CommSemiring T] [Algebra T B]
    [CommSemiring R] [Algebra R S] [Algebra R T] [Algebra R A] [Algebra R B]
    [IsScalarTower R S A] [IsScalarTower R T B]
    [DecidableEq ╬╣] [AddMonoid ╬╣]
    (­Юњю : ╬╣ Рєњ Submodule S A) (Рёг : ╬╣ Рєњ Submodule T B) [GradedAlgebra ­Юњю] [GradedAlgebra Рёг]
    extends A РЅЃРѓљ[R] B, ­Юњю РЅЃ+*рхЇ Рёг

@[inherit_doc]
notation:25 ­Юњю " РЅЃРѓљрхЇ[" R "] " Рёг => GradedAlgEquiv R ­Юњю Рёг

/-- The underlying algebra isomorphism. -/
add_decl_doc GradedAlgEquiv.toAlgEquiv

/-- The underlying graded ring isomorphism. -/
add_decl_doc GradedAlgEquiv.toGradedRingEquiv

namespace GradedAlgEquiv

section Semiring
variable [Semiring A] [Semiring B] [Semiring C] [Semiring D]
  [CommSemiring S] [Algebra S A] [CommSemiring T] [Algebra T B]
  [CommSemiring U] [Algebra U C] [CommSemiring V] [Algebra V D]
  [CommSemiring R] [Algebra R S] [Algebra R T] [Algebra R U] [Algebra R V]
  [Algebra R A] [Algebra R B] [Algebra R C] [Algebra R D]
  [IsScalarTower R S A] [IsScalarTower R T B] [IsScalarTower R U C] [IsScalarTower R V D]
  [DecidableEq ╬╣] [AddMonoid ╬╣]
  {­Юњю : ╬╣ Рєњ Submodule S A} {Рёг : ╬╣ Рєњ Submodule T B}
  {­Юњъ : ╬╣ Рєњ Submodule U C} {­ЮњЪ : ╬╣ Рєњ Submodule V D}
  [GradedAlgebra ­Юњю] [GradedAlgebra Рёг] [GradedAlgebra ­Юњъ] [GradedAlgebra ­ЮњЪ]

/-- Turn an element of a type `E` satisfying `GradedEquivLike E ­Юњю Рёг` and `AlgEquivClass E R A B`
into an actual `GradedAlgEquiv`. This is declared as the default coercion from `E` to
`­Юњю РЅЃРѓљрхЇ[R] Рёг`. -/
@[coe]
def ofClass {E : Type*} [GradedEquivLike E ­Юњю Рёг] [AlgEquivClass E R A B] (e : E) : ­Юњю РЅЃРѓљрхЇ[R] Рёг :=
  { (e : ­Юњю РЅЃ+*рхЇ Рёг), (e : ­Юњю РєњРѓљрхЇ[R] Рёг) with }

instance {E : Type*} [GradedEquivLike E ­Юњю Рёг] [AlgEquivClass E R A B] : CoeTC E (­Юњю РЅЃРѓљрхЇ[R] Рёг) :=
  РЪеofClassРЪЕ

section coe

instance : GradedEquivLike (­Юњю РЅЃРѓљрхЇ[R] Рёг) ­Юњю Рёг where
  coe f := f.toFun
  inv f := f.invFun
  coe_injective' e f hРѓЂ hРѓѓ := by
    cases e
    cases f
    congr 1
    exact AlgEquiv.ext (congr($hРѓЂ ┬и))
  left_inv f := f.left_inv
  right_inv f := f.right_inv
  map_mem_iff e {_ _} := map_mem_iff e.toGradedRingEquiv

instance : AlgEquivClass (­Юњю РЅЃРѓљрхЇ[R] Рёг) R A B where
  map_add f := f.map_add'
  map_mul f := f.map_mul'
  commutes f := f.commutes

/-- Two graded ring isomorphisms agree if they are defined by the same underlying function. -/
@[ext]
theorem ext {f g : ­Юњю РЅЃРѓљрхЇ[R] Рёг} (h : Рѕђ x, f x = g x) : f = g :=
  DFunLike.ext f g h

/-- Consider using `congr(f $h)`. -/
protected theorem congr_arg {f : ­Юњю РЅЃРѓљрхЇ[R] Рёг} {x x' : A} : x = x' Рєњ f x = f x' :=
  DFunLike.congr_arg f

/-- Consider using `congr($h x)`. -/
protected theorem congr_fun {f g : ­Юњю РЅЃРѓљрхЇ[R] Рёг} (h : f = g) (x : A) : f x = g x :=
  DFunLike.congr_fun h x

@[simp] theorem coe_mk (e h) : РЄЉ(РЪеe, hРЪЕ : ­Юњю РЅЃРѓљрхЇ[R] Рёг) = e := rfl

@[simp]
theorem mk_coe (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) (e' hРѓЂ hРѓѓ hРѓЃ hРѓё hРѓЁ hРѓє) :
    (РЪеРЪеРЪеe, e', hРѓЂ, hРѓѓРЪЕ, hРѓЃ, hРѓё, hРѓЁРЪЕ, hРѓєРЪЕ : ­Юњю РЅЃРѓљрхЇ[R] Рёг) = e := ext fun _ => rfl

@[simp] theorem toGRingEquiv_eq_coe (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : f.toGradedRingEquiv = РєЉf := rfl

@[simp] theorem toAlgEquiv_eq_coe (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : f.toAlgEquiv = РєЉf := rfl

@[simp] theorem coe_toEquiv (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : РЄЉ(f : A РЅЃ B) = f := rfl

@[simp] theorem coe_toAddEquiv (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : РЄЉ(f : A РЅЃ+ B) = f := rfl

@[simp, norm_cast]
theorem coe_toMulEquiv (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : РЄЉ(f : A РЅЃ* B) = f := rfl

@[simp] theorem coe_toRingEquiv (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : РЄЉ(f : A РЅЃ+* B) = f := rfl

@[simp] theorem coe_toAlgEquiv (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : РЄЉ(f : A РЅЃРѓљ[R] B) = f := rfl

@[simp] theorem coe_toGRingEquiv (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : РЄЉ(f : ­Юњю РЅЃ+*рхЇ Рёг) = f := rfl

@[simp] theorem coe_toGRingHom (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : РЄЉ(f : ­Юњю Рєњ+*рхЇ Рёг) = f := rfl

@[simp] theorem coe_toGAlgHom (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : РЄЉ(f : ­Юњю РєњРѓљрхЇ[R] Рёг) = f := rfl

theorem coe_injective : Function.Injective ((РєЉ) : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ A Рєњ B) :=
  DFunLike.coe_injective'

theorem coe_gRingHom_injective : Function.Injective ((РєЉ) : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ ­Юњю Рєњ+*рхЇ Рёг) :=
  fun _ _ h Рєд coe_injective congr($h)

theorem coe_ringHom_injective : Function.Injective ((РєЉ) : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ A Рєњ+* B) :=
  fun _ _ h Рєд coe_injective congr($h)

theorem coe_monoidHom_injective : Function.Injective ((РєЉ) : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ A Рєњ* B) :=
  fun _ _ h Рєд coe_injective congr($h)

theorem coe_addMonoidHom_injective : Function.Injective ((РєЉ) : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ A Рєњ+ B) :=
  fun _ _ h Рєд coe_injective congr($h)

theorem coe_ringEquiv_injective : Function.Injective ((РєЉ) : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ A РЅЃ+* B) :=
  fun _ _ h Рєд coe_injective congr($h)

theorem coe_mulEquiv_injective : Function.Injective ((РєЉ) : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ A РЅЃ* B) :=
  fun _ _ h Рєд coe_injective congr($h)

theorem coe_addEquiv_injective : Function.Injective ((РєЉ) : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ A РЅЃ+ B) :=
  fun _ _ h Рєд coe_injective congr($h)

theorem coe_equiv_injective : Function.Injective ((РєЉ) : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ A РЅЃ B) :=
  fun _ _ h Рєд coe_injective congr($h)

end coe

section map
variable (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг)

/-- A graded ring isomorphism preserves zero. -/
protected theorem map_zero : e 0 = 0 :=
  map_zero e

/-- A graded ring isomorphism preserves one. -/
protected theorem map_one : e 1 = 1 :=
  map_one e

/-- A graded ring isomorphism preserves addition. -/
protected theorem map_add (x y : A) : e (x + y) = e x + e y :=
  map_add e x y

/-- A graded ring isomorphism preserves multiplication. -/
protected theorem map_mul (x y : A) : e (x * y) = e x * e y :=
  map_mul e x y

protected theorem map_pow (x : A) (n : РёЋ) : e (x ^ n) = e x ^ n :=
  map_pow e x n

protected theorem map_eq_zero_iff (x : A) : e x = 0 Рєћ x = 0 :=
  e.toRingEquiv.map_eq_zero_iff

protected theorem map_ne_zero_iff (x : A) : e x РЅа 0 Рєћ x РЅа 0 :=
  e.toRingEquiv.map_ne_zero_iff

protected theorem map_eq_one_iff (x : A) : e x = 1 Рєћ x = 1 :=
  e.toRingEquiv.map_eq_one_iff

protected theorem map_ne_one_iff (x : A) : e x РЅа 1 Рєћ x РЅа 1 :=
  e.toRingEquiv.map_ne_one_iff

end map

section bijective

protected theorem bijective (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : Function.Bijective e :=
  EquivLike.bijective e

protected theorem injective (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : Function.Injective e :=
  EquivLike.injective e

protected theorem surjective (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : Function.Surjective e :=
  EquivLike.surjective e

end bijective

section symm

/-- The inverse of a graded ring isomorphism is a graded ring isomorphism. -/
@[symm] protected def symm (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : Рёг РЅЃРѓљрхЇ[R] ­Юњю :=
  { e.toAlgEquiv.symm, e.toGradedRingEquiv.symm with }

@[simp] theorem invFun_eq_symm (f : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : EquivLike.inv f = f.symm := rfl

@[simp] theorem symm_symm (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : e.symm.symm = e := rfl

theorem symm_bijective : Function.Bijective (GradedAlgEquiv.symm : (­Юњю РЅЃРѓљрхЇ[R] Рёг) Рєњ Рёг РЅЃРѓљрхЇ[R] ­Юњю) :=
  Function.bijective_iff_has_inverse.mpr РЪе_, symm_symm, symm_symmРЪЕ

@[simp]
theorem mk_coe' (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) (f hРѓЂ hРѓѓ hРѓЃ hРѓё hРѓЁ hРѓє) :
    (РЪеРЪеРЪеf, РЄЉe, hРѓЂ, hРѓѓРЪЕ, hРѓЃ, hРѓё, hРѓЁРЪЕ, hРѓєРЪЕ : Рёг РЅЃРѓљрхЇ[R] ­Юњю) = e.symm :=
  symm_bijective.injective <| ext fun _ Рєд rfl

/-- Auxiliary definition to avoid looping in `dsimp` with `RingEquiv.symm_mk`. -/
protected def symm_mk.aux (f : B Рєњ A) (g hРѓЂ hРѓѓ hРѓЃ hРѓё hРѓЁ hРѓє) :=
  (mk (R := R) (­Юњю := Рёг) (Рёг := ­Юњю) РЪеРЪеf, g, hРѓЂ, hРѓѓРЪЕ, hРѓЃ, hРѓё, hРѓЁРЪЕ hРѓє).symm

@[simp]
theorem symm_mk (f : B Рєњ A) (g hРѓЂ hРѓѓ hРѓЃ hРѓё hРѓЁ hРѓє) :
    (mk РЪеРЪеf, g, hРѓЂ, hРѓѓРЪЕ, hРѓЃ, hРѓё, hРѓЁРЪЕ hРѓє).symm =
      { symm_mk.aux (R := R) (­Юњю := ­Юњю) (Рёг := Рёг) f g hРѓЂ hРѓѓ hРѓЃ hРѓё hРѓЁ hРѓє with
        toFun := g
        invFun := f } :=
  rfl

@[simp] theorem coe_toEquiv_symm (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : (e.symm : B РЅЃ A) = (e : A РЅЃ B).symm := rfl

@[simp]
theorem coe_toMulEquiv_symm (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : (e.symm : B РЅЃ* A) = (e : A РЅЃ* B).symm := rfl

@[simp]
theorem coe_toAddEquiv_symm (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : (e.symm : B РЅЃ+ A) = (e : A РЅЃ+ B).symm := rfl

@[simp]
theorem coe_toRingEquiv_symm (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : (e.symm : B РЅЃ* A) = (e : A РЅЃ* B).symm := rfl

@[simp]
theorem apply_symm_apply (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : Рѕђ x, e (e.symm x) = x :=
  e.toEquiv.apply_symm_apply

@[simp]
theorem symm_apply_apply (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : Рѕђ x, e.symm (e x) = x :=
  e.toEquiv.symm_apply_apply

theorem image_eq_preimage (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) (s : Set A) : e '' s = e.symm РЂ╗┬╣' s :=
  e.toEquiv.image_eq_preimage s

theorem symm_apply_eq (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) {x : B} {y : A} :
    e.symm x = y Рєћ x = e y := Equiv.symm_apply_eq _

theorem eq_symm_apply (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) {x : B} {y : A} :
    y = e.symm x Рєћ e y = x := Equiv.eq_symm_apply _

end symm

section Simps

/-- See Note [custom simps projection] -/
def Simps.apply (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : A Рєњ B := РЄЉe

/-- See Note [custom simps projection] -/
def Simps.symm_apply (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : B Рєњ A := РЄЉe.symm

initialize_simps_projections GradedAlgEquiv (toFun Рєњ apply, invFun Рєњ symm_apply)

end Simps

section refl

variable (R ­Юњю) in
/-- The identity map as a graded ring isomorphism. -/
@[simps!] protected def refl : ­Юњю РЅЃРѓљрхЇ[R] ­Юњю :=
  { AlgEquiv.refl, GradedRingEquiv.refl ­Юњю with }

@[simp] theorem symm_refl : (GradedAlgEquiv.refl R ­Юњю).symm = .refl R ­Юњю := rfl

@[simp] theorem coe_refl : РЄЉ(GradedAlgEquiv.refl R ­Юњю) = id := rfl

@[simp] theorem coe_toRingEquiv_refl : (GradedAlgEquiv.refl R ­Юњю : A РЅЃ+* A) = .refl A := rfl

@[simp] theorem coe_addEquiv_refl : (GradedAlgEquiv.refl R ­Юњю : A РЅЃ+ A) = AddEquiv.refl A := rfl

@[simp] theorem coe_mulEquiv_refl : (GradedAlgEquiv.refl R ­Юњю : A РЅЃ* A) = MulEquiv.refl A := rfl

@[simp] theorem toEquiv_refl : GradedAlgEquiv.refl R ­Юњю = Equiv.refl A := rfl

@[simp]
theorem coe_gRingHom_refl : (GradedAlgEquiv.refl R ­Юњю : ­Юњю Рєњ+*рхЇ ­Юњю) = .id ­Юњю := rfl

@[simp] theorem coe_ringHom_refl : (GradedAlgEquiv.refl R ­Юњю : A Рєњ+* A) = .id A := rfl

@[simp] theorem coe_monoidHom_refl : (GradedAlgEquiv.refl R ­Юњю : A Рєњ* A) = .id A := rfl

@[simp] theorem coe_addMonoidHom_refl : (GradedAlgEquiv.refl R ­Юњю : A Рєњ+ A) = .id A := rfl

end refl

section trans
variable (eРѓЂ : ­Юњю РЅЃРѓљрхЇ[R] Рёг) (eРѓѓ : Рёг РЅЃРѓљрхЇ[R] ­Юњъ)

/-- The composition of two graded ring isomorphisms. -/
@[trans, simps! apply] protected def trans (eРѓЂ : ­Юњю РЅЃРѓљрхЇ[R] Рёг) (eРѓѓ : Рёг РЅЃРѓљрхЇ[R] ­Юњъ) : ­Юњю РЅЃРѓљрхЇ[R] ­Юњъ :=
  { eРѓЂ.toAlgEquiv.trans eРѓѓ.toAlgEquiv, eРѓЂ.toGradedRingEquiv.trans eРѓѓ.toGradedRingEquiv with }

@[simp] theorem coe_trans : РЄЉ(eРѓЂ.trans eРѓѓ) = eРѓѓ Рѕў eРѓЂ := rfl

theorem symm_trans_apply (a : C) : (eРѓЂ.trans eРѓѓ).symm a = eРѓЂ.symm (eРѓѓ.symm a) := rfl

@[simp] theorem symm_trans : (eРѓЂ.trans eРѓѓ).symm = eРѓѓ.symm.trans eРѓЂ.symm := rfl

@[simp] theorem coe_ringEquiv_trans : (eРѓЂ.trans eРѓѓ : A РЅЃ+* C) = (eРѓЂ : A РЅЃ+* B).trans РєЉeРѓѓ := rfl

@[simp] theorem coe_mulEquiv_trans : (eРѓЂ.trans eРѓѓ : A РЅЃ* C) = (eРѓЂ : A РЅЃ* B).trans РєЉeРѓѓ := rfl

@[simp] theorem coe_addEquiv_trans : (eРѓЂ.trans eРѓѓ : A РЅЃ+ C) = (eРѓЂ : A РЅЃ+ B).trans РєЉeРѓѓ := rfl

@[simp] theorem coe_gRingHom_trans : (eРѓЂ.trans eРѓѓ : ­Юњю Рєњ+*рхЇ ­Юњъ) = (eРѓѓ : Рёг Рєњ+*рхЇ ­Юњъ).comp РєЉeРѓЂ := rfl

@[simp] theorem coe_ringHom_trans : (eРѓЂ.trans eРѓѓ : A Рєњ+* C) = (eРѓѓ : B Рєњ+* C).comp РєЉeРѓЂ := rfl

@[simp] theorem coe_monoidHom_trans : (eРѓЂ.trans eРѓѓ : A Рєњ* C) = (eРѓѓ : B Рєњ* C).comp РєЉeРѓЂ := rfl

@[simp] theorem coe_addMonoidHom_trans : (eРѓЂ.trans eРѓѓ : A Рєњ+ C) = (eРѓѓ : B Рєњ+ C).comp РєЉeРѓЂ := rfl

@[simp] theorem self_trans_symm : eРѓЂ.trans eРѓЂ.symm = .refl R ­Юњю :=
  coe_equiv_injective eРѓЂ.toEquiv.self_trans_symm

@[simp] theorem symm_trans_self : eРѓЂ.symm.trans eРѓЂ = .refl R Рёг :=
  coe_equiv_injective eРѓЂ.toEquiv.symm_trans_self

end trans

section ofBijective

variable {F : Type*} [GradedFunLike F ­Юњю Рёг] [AlgHomClass F R A B]

/-- Produce a graded ring isomorphism from a bijective graded ring homomorphism. -/
noncomputable def ofBijective (f : F) (hf : Function.Bijective f) : ­Юњю РЅЃРѓљрхЇ[R] Рёг :=
  { AlgEquiv.ofBijective (f : A РєњРѓљ[R] B) hf, GradedRingEquiv.ofBijective f hf with }

variable (f : F) (hf : Function.Bijective f)

@[simp] theorem coe_ofBijective : РЄЉ(ofBijective f hf) = f := rfl

@[simp] theorem coe_toGAlgHom_ofBijective : (ofBijective f hf : ­Юњю РєњРѓљрхЇ[R] Рёг) = f := rfl

theorem ofBijective_apply (x : A) : ofBijective f hf x = f x := rfl

@[simp]
lemma ofBijective_symm_comp (f : ­Юњю РєњРѓљрхЇ[R] Рёг) (hf : Function.Bijective f) :
    ((ofBijective f hf).symm : Рёг РєњРѓљрхЇ[R] ­Юњю).comp f = .id R ­Юњю :=
  GradedAlgHom.ext fun _ Рєд (ofBijective f hf).injective <| apply_symm_apply ..

@[simp]
lemma comp_ofBijective_symm (f : ­Юњю РєњРѓљрхЇ[R] Рёг) (hf : Function.Bijective f) :
    f.comp ((ofBijective f hf).symm : Рёг РєњРѓљрхЇ[R] ­Юњю) = .id R Рёг :=
  GradedAlgHom.ext fun _ Рєд (ofBijective f hf).symm.injective <| apply_symm_apply ..

@[simp]
theorem comp_symm (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : (e : ­Юњю РєњРѓљрхЇ[R] Рёг).comp (e.symm : Рёг РєњРѓљрхЇ[R] ­Юњю) = .id R Рёг :=
  GradedAlgHom.ext e.apply_symm_apply

@[simp]
theorem symm_comp (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) : (e.symm : Рёг РєњРѓљрхЇ[R] ­Юњю).comp (e : ­Юњю РєњРѓљрхЇ[R] Рёг) = .id R ­Юњю :=
  GradedAlgHom.ext e.symm_apply_apply

end ofBijective

/-- Construct a mutually-inverse pair of graded ring homomorphisms into a graded ring isomorphism.
-/
def ofGRingHom (f : ­Юњю РєњРѓљрхЇ[R] Рёг) (g : Рёг РєњРѓљрхЇ[R] ­Юњю) (hРѓЂ : g.comp f = GradedRingHom.id ­Юњю)
    (hРѓѓ : f.comp g = GradedRingHom.id Рёг) : ­Юњю РЅЃРѓљрхЇ[R] Рёг where
  __ := f
  __ := RingEquiv.ofRingHom f.toRingHom g.toRingHom congr($hРѓѓ) congr($hРѓЂ)

@[simp] lemma coe_ofGRingHom (f : ­Юњю РєњРѓљрхЇ[R] Рёг) (g hРѓЂ hРѓѓ) :
    РЄЉ(ofGRingHom f g hРѓЂ hРѓѓ : ­Юњю РЅЃРѓљрхЇ[R] Рёг) = f := rfl

@[simp] lemma toGRingHom_ofGRingHom (f : ­Юњю РєњРѓљрхЇ[R] Рёг) (g hРѓЂ hРѓѓ) :
    (ofGRingHom f g hРѓЂ hРѓѓ : ­Юњю РєњРѓљрхЇ[R] Рёг) = f := rfl

@[simp] lemma toMonoidHom_ofGRingHom (f : ­Юњю РєњРѓљрхЇ[R] Рёг) (g hРѓЂ hРѓѓ) :
    (ofGRingHom f g hРѓЂ hРѓѓ : A Рєњ* B) = f := rfl

@[simp] lemma toAddMonoidHom_ofGRingHom (f : ­Юњю РєњРѓљрхЇ[R] Рёг) (g hРѓЂ hРѓѓ) :
    (ofGRingHom f g hРѓЂ hРѓѓ : A Рєњ+ B) = f := rfl

@[simp] lemma symm_ofGRingHom (f : ­Юњю РєњРѓљрхЇ[R] Рёг) (g hРѓЂ hРѓѓ) :
    (ofGRingHom f g hРѓЂ hРѓѓ).symm = ofGRingHom g f hРѓѓ hРѓЂ := rfl

end Semiring

section Ring
variable [Ring A] [Ring B]
  [CommSemiring S] [Algebra S A] [CommSemiring T] [Algebra T B]
  [CommSemiring R] [Algebra R S] [Algebra R T]
  [Algebra R A] [Algebra R B]
  [IsScalarTower R S A] [IsScalarTower R T B]
  [DecidableEq ╬╣] [AddMonoid ╬╣]
  {­Юњю : ╬╣ Рєњ Submodule S A} {Рёг : ╬╣ Рєњ Submodule T B}
  [GradedAlgebra ­Юњю] [GradedAlgebra Рёг]
  (e : ­Юњю РЅЃРѓљрхЇ[R] Рёг) (x y : A)

protected theorem map_neg : e (-x) = -e x :=
  map_neg e x

protected theorem map_sub : e (x - y) = e x - e y :=
  map_sub e x y

protected theorem map_neg_one : e (-1) = -1 :=
  e.toAlgEquiv.map_neg_one

protected theorem map_eq_neg_one_iff {x : A} : e x = -1 Рєћ x = -1 :=
  e.toAlgEquiv.map_eq_neg_one_iff

end Ring

end GradedAlgEquiv
