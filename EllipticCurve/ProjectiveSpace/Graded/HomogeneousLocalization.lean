/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import EllipticCurve.ProjectiveSpace.Graded.AlgHom
import Mathlib.RingTheory.GradedAlgebra.HomogeneousLocalization

namespace SetLike.GradeZero

instance instAlgebra' {Î¹ R A : Type*} [CommSemiring R] [Semiring A] [Algebra R A]
    [DecidableEq Î¹] [AddMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedMonoid ğ’œ]
    (Râ‚€ : Type*) [CommSemiring Râ‚€] [Algebra Râ‚€ R] [Algebra Râ‚€ A] [IsScalarTower Râ‚€ R A] :
    Algebra Râ‚€ (ğ’œ 0) where
  algebraMap := (algebraMap R (ğ’œ 0)).comp (algebraMap Râ‚€ R)
  commutes' _ _ := Algebra.commutes (algebraMap Râ‚€ R _) _
  smul_def' r x := Subtype.ext <| by
    rw [SetLike.val_smul_of_tower, â† algebraMap_smul R, Algebra.smul_def]; rfl

variable {Î¹ R A : Type*} [CommSemiring R] [Semiring A] [Algebra R A]
  [DecidableEq Î¹] [AddMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedMonoid ğ’œ]
  (Râ‚€ : Type*) [CommSemiring Râ‚€] [Algebra Râ‚€ R] [Algebra Râ‚€ A] [IsScalarTower Râ‚€ R A]

instance instIsScalarTowerâ‚ : IsScalarTower Râ‚€ R (ğ’œ 0) where
  smul_assoc x y z := by simp [Algebra.smul_def, â† IsScalarTower.algebraMap_apply, mul_assoc]

instance instIsScalarTowerâ‚‚ : IsScalarTower Râ‚€ (ğ’œ 0) A where
  smul_assoc _ _ _ := Algebra.smul_mul_assoc ..

@[simp] theorem algebraMap_coe
    (x : Râ‚€) : algebraMap Râ‚€ (ğ’œ 0) x = algebraMap Râ‚€ A x :=
  (IsScalarTower.algebraMap_apply ..).symm

end SetLike.GradeZero

@[simp] theorem Localization.localRingHom_mk {R : Type*} [CommSemiring R]
    {P : Type*} [CommSemiring P]
    (I : Ideal R) [hI : I.IsPrime] (J : Ideal P) [J.IsPrime]
    (f : R â†’+* P) (hIJ : I = Ideal.comap f J) (x : R) (y : â†¥I.primeCompl) :
    (localRingHom I J f hIJ) (mk x y) =
      mk (f x) âŸ¨f y, le_comap_primeCompl_iff.mpr (ge_of_eq hIJ) y.2âŸ© := by
  simp [mk_eq_mk', localRingHom_mk']

namespace HomogeneousLocalization

open SetLike

section Algebra

@[simp] lemma val_fromZeroRingHom {Î¹ A Ïƒ : Type*} [CommRing A]
    [SetLike Ïƒ A] [AddSubgroupClass Ïƒ A]
    [AddCommMonoid Î¹] [DecidableEq Î¹] (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ] (x : Submonoid A) (f : ğ’œ 0) :
    (fromZeroRingHom ğ’œ x f).val = .mk f 1 :=
  rfl

variable {Î¹ R A : Type*} [CommRing R] [CommRing A] [Algebra R A]
  [DecidableEq Î¹] [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedRing ğ’œ] (x : Submonoid A)
  (Râ‚€ : Type*) [CommSemiring Râ‚€] [Algebra Râ‚€ R] [Algebra Râ‚€ A] [IsScalarTower Râ‚€ R A]

instance : SMul Râ‚€ (HomogeneousLocalization ğ’œ x) :=
  have : SMulMemClass (Submodule R A) Râ‚€ A := SMulMemClass.ofIsScalarTower ..
  inferInstance

instance : Algebra Râ‚€ (HomogeneousLocalization ğ’œ x) where
  algebraMap := (fromZeroRingHom ğ’œ x).comp <| algebraMap Râ‚€ (ğ’œ 0)
  commutes' _ _ := mul_comm _ _
  smul_def' r z := by
    obtain âŸ¨z, rflâŸ© := z.mk_surjective
    ext
    simp [Localization.smul_mk, Localization.mk_mul, â† Algebra.smul_def]

instance : Module Râ‚€ (HomogeneousLocalization ğ’œ x) :=
  inferInstance

lemma algebraMap_apply' {r : Râ‚€} : algebraMap Râ‚€ (HomogeneousLocalization ğ’œ x) r =
    fromZeroRingHom ğ’œ x (algebraMap Râ‚€ (ğ’œ 0) r) := rfl

instance : IsScalarTower Râ‚€ R (HomogeneousLocalization ğ’œ x) :=
  .of_algebraMap_eq' rfl

end Algebra

section GradedRing
variable {Î¹ A Ïƒ : Type*} [CommRing A] [SetLike Ïƒ A] [AddSubgroupClass Ïƒ A]
  [DecidableEq Î¹] [AddCommMonoid Î¹]
  (ğ’œ : Î¹ â†’ Ïƒ) [GradedRing ğ’œ] {s : Submonoid A} {f : A}

-- bundled AddHom
def of {i : Î¹} {d : ğ’œ i} (hd : â†‘d âˆˆ s) : ğ’œ i â†’+ HomogeneousLocalization ğ’œ s where
  toFun x := mk âŸ¨i, x, d, hdâŸ©
  map_add' x y := by ext; simp [Localization.add_mk_self]
  map_zero' := by ext; simp [Localization.mk_zero]

namespace Away
variable {i : Î¹} (hf : f âˆˆ ğ’œ i) (n : â„•)

-- bundled AddHom, default constructor
nonrec def of : ğ’œ (n â€¢ i) â†’+ Away ğ’œ f :=
  of ğ’œ (d := âŸ¨f ^ n, SetLike.pow_mem_graded _ hfâŸ©) âŸ¨n, rflâŸ©

@[simp] theorem val_of (a : ğ’œ (n â€¢ i)) : (of ğ’œ hf n a).val = .mk a âŸ¨f ^ n, n, rflâŸ© := rfl

theorem of_surjective {i : Î¹} (hf : f âˆˆ ğ’œ i) (x : Away ğ’œ f) :
    âˆƒ n num, of ğ’œ hf n num = x :=
  let âŸ¨n, num, num_mem, hxâŸ© := x.mk_surjective ğ’œ hf; âŸ¨n, âŸ¨num, num_memâŸ©, hxâŸ©

end Away

end GradedRing

section GradedAlgebra
variable {Î¹ R A : Type*}
variable [DecidableEq Î¹] [AddCommMonoid Î¹]
variable [CommRing R] [CommRing A] [Algebra R A]
variable (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
variable {x : Submonoid A}

/-- Given a denominator `den : ğ’œ i`, we have a linear map `ğ’œ i â†’ HomogeneousLocalization ğ’œ x` that
sends `n` to `n/den`. -/
def mkâ‚— {i : Î¹} {den : ğ’œ i} (den_mem : (den : A) âˆˆ x) :
    ğ’œ i â†’â‚—[R] HomogeneousLocalization ğ’œ x where
  toFun num := mk âŸ¨_, num, den, den_memâŸ©
  map_add' _ _ := by ext; simp [Localization.add_mk_self]
  map_smul' _ _ := by ext; simp [Localization.smul_mk]

@[simp] lemma mkâ‚—_apply {i : Î¹} {den : ğ’œ i} (den_mem : (den : A) âˆˆ x) (n : ğ’œ i) :
    mkâ‚— ğ’œ den_mem n = of ğ’œ den_mem n := rfl

/-- Given `n : â„•`, we have a linear map `ğ’œ (n â€¢ d) â†’ HomogeneousLocalization ğ’œ x` that sends `x`
to `x / f ^ n`. -/
nonrec def Away.mkâ‚— {f : A} {d : Î¹} (hf : f âˆˆ ğ’œ d) (n : â„•) :
    ğ’œ (n â€¢ d) â†’â‚—[R] HomogeneousLocalization.Away ğ’œ f :=
  mkâ‚— ğ’œ (den := âŸ¨f ^ n, pow_mem_graded _ hfâŸ©) âŸ¨n, rflâŸ©

@[simp] lemma Away.mkâ‚—_apply {f : A} {d : Î¹} (hf : f âˆˆ ğ’œ d) {n : â„•} (x : ğ’œ (n â€¢ d)) :
    Away.mkâ‚— ğ’œ hf n x = .of ğ’œ hf n x := rfl

theorem Away.hom_ext {f : A} {d : Î¹} (hf : f âˆˆ ğ’œ d)
    {M : Type*} [AddCommGroup M] [Module R M] {gâ‚ gâ‚‚ : Away ğ’œ f â†’â‚—[R] M}
    (h : âˆ€ n, gâ‚ âˆ˜â‚— Away.mkâ‚— ğ’œ hf n = gâ‚‚ âˆ˜â‚— Away.mkâ‚— ğ’œ hf n) : gâ‚ = gâ‚‚ :=
  LinearMap.ext fun x â†¦ let âŸ¨n, num, hxâŸ© := x.of_surjective ğ’œ hf; hx â–¸ congr($(h n) num)

end GradedAlgebra

section GradedRingHom
variable {Î¹ Aâ‚ Aâ‚‚ Ïƒâ‚ Ïƒâ‚‚ : Type*} [CommRing Aâ‚] [CommRing Aâ‚‚]
  [SetLike Ïƒâ‚ Aâ‚] [AddSubgroupClass Ïƒâ‚ Aâ‚] [SetLike Ïƒâ‚‚ Aâ‚‚] [AddSubgroupClass Ïƒâ‚‚ Aâ‚‚]
  [DecidableEq Î¹] [AddCommMonoid Î¹]
  {ğ’œâ‚ : Î¹ â†’ Ïƒâ‚} [GradedRing ğ’œâ‚] {ğ’œâ‚‚ : Î¹ â†’ Ïƒâ‚‚} [GradedRing ğ’œâ‚‚]
  {F : Type*} [GradedFunLike F ğ’œâ‚ ğ’œâ‚‚] [RingHomClass F Aâ‚ Aâ‚‚] (f : F)

section
variable {P : Submonoid Aâ‚} {Q : Submonoid Aâ‚‚} (comap_le : P â‰¤ Q.comap f)

def map' : HomogeneousLocalization ğ’œâ‚ P â†’+* HomogeneousLocalization ğ’œâ‚‚ Q :=
  map _ _ f comap_le fun _ _ â†¦ map_mem f

lemma map'_mk (c : NumDenSameDeg ğ’œâ‚ P) :
    map' f comap_le (mk c) =
    mk âŸ¨c.deg, âŸ¨_, map_mem f c.num.2âŸ©, âŸ¨_, map_mem f c.den.2âŸ©, comap_le c.den_memâŸ© := rfl

end

namespace Away
variable {xâ‚ : Aâ‚} {xâ‚‚ : Aâ‚‚} (hfx : f xâ‚ = xâ‚‚)

def map : Away ğ’œâ‚ xâ‚ â†’+* Away ğ’œâ‚‚ xâ‚‚ :=
  map' f <| Submonoid.powers_le.mpr âŸ¨1, by simp [hfx]âŸ©

@[simp] lemma map_of {d : Î¹} (hx : xâ‚ âˆˆ ğ’œâ‚ d) (n : â„•) (a : ğ’œâ‚ (n â€¢ d)) :
    map f hfx (.of ğ’œâ‚ hx n a) = .of ğ’œâ‚‚ (hfx â–¸ map_mem f hx) n (gradedAddHom f (n â€¢ d) a) := by
  simp [map, of, HomogeneousLocalization.of, map'_mk, gradedAddHom, hfx]

-- lemma val_map {d : Î¹} (hx : xâ‚ âˆˆ ğ’œâ‚ d) (a : Away ğ’œâ‚ xâ‚) :
--     (map f hfx a).val = Localization.awayLift ((algebraMap _ _).comp f.toRingHom) _
--       (IsLocalization.map_units (M := .powers xâ‚‚) _ âŸ¨f xâ‚, 1, hfx â–¸ pow_one _âŸ©) a.val := by
--   obtain âŸ¨n, a, ha, rflâŸ© := a.of_surjective _ hx
--   simp [Localization.awayLift_mk]

end Away

noncomputable def localRingHom (I : Ideal Aâ‚) [I.IsPrime] (J : Ideal Aâ‚‚) [J.IsPrime]
    (hIJ : I = J.comap f) :
    AtPrime ğ’œâ‚ I â†’+* AtPrime ğ’œâ‚‚ J :=
  map' f <| (Localization.le_comap_primeCompl_iff (f := RingHomClass.toRingHom f)).mpr <|
    hIJ â–¸ le_rfl

variable (I : Ideal Aâ‚) [I.IsPrime] (J : Ideal Aâ‚‚) [J.IsPrime] (hIJ : I = J.comap f)

@[simp] lemma val_localRingHom (x : AtPrime ğ’œâ‚ I) :
    (localRingHom f I J hIJ x).val = Localization.localRingHom _ _ f hIJ x.val := by
  obtain âŸ¨âŸ¨i, x, s, hsâŸ©, rflâŸ© := x.mk_surjective
  simp [localRingHom, map'_mk]

instance isLocalHom_localRingHom : IsLocalHom (localRingHom f I J hIJ) where
  map_nonunit x hx := by
    rw [â† isUnit_iff_isUnit_val] at hx âŠ¢
    rw [val_localRingHom] at hx
    exact IsLocalHom.map_nonunit _ hx

@[simps] def NumDenSameDeg.map {Wâ‚ : Submonoid Aâ‚} {Wâ‚‚ : Submonoid Aâ‚‚}
    (hw : Wâ‚ â‰¤ Wâ‚‚.comap f) (c : NumDenSameDeg ğ’œâ‚ Wâ‚) : NumDenSameDeg ğ’œâ‚‚ Wâ‚‚ where
  deg := c.deg
  den := gradedAddHom f _ c.den
  num := gradedAddHom f _ c.num
  den_mem := hw c.den_mem

lemma localRingHom_mk (c : NumDenSameDeg ğ’œâ‚ I.primeCompl) :
    localRingHom f I J hIJ (.mk c) =
      .mk (c.map f <| hIJ â–¸ by rfl) := by
  rfl

end GradedRingHom

section GradedAlgHom

variable {R Râ‚ Râ‚‚ Aâ‚ Aâ‚‚ : Type*}
  [CommRing R] [CommRing Râ‚] [CommRing Râ‚‚] [CommRing Aâ‚] [CommRing Aâ‚‚]
  [Algebra R Râ‚] [Algebra Râ‚ Aâ‚] [Algebra R Aâ‚] [IsScalarTower R Râ‚ Aâ‚]
  [Algebra R Râ‚‚] [Algebra Râ‚‚ Aâ‚‚] [Algebra R Aâ‚‚] [IsScalarTower R Râ‚‚ Aâ‚‚]
  {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹]
  {ğ’œâ‚ : Î¹ â†’ Submodule Râ‚ Aâ‚} [GradedAlgebra ğ’œâ‚]
  {ğ’œâ‚‚ : Î¹ â†’ Submodule Râ‚‚ Aâ‚‚} [GradedAlgebra ğ’œâ‚‚]
  (g : ğ’œâ‚ â†’â‚áµ[R] ğ’œâ‚‚) {ğ’®â‚ : Submonoid Aâ‚} {ğ’®â‚‚ : Submonoid Aâ‚‚}
  (comap_le : ğ’®â‚ â‰¤ Submonoid.comap g ğ’®â‚‚)

open NumDenSameDeg in
def mapâ‚ : HomogeneousLocalization ğ’œâ‚ ğ’®â‚ â†’â‚[R] HomogeneousLocalization ğ’œâ‚‚ ğ’®â‚‚ where
  __ := map' g comap_le
  commutes' r := by ext; simp [map'_mk, algebraMap_apply', fromZeroRingHom]

@[simp] lemma mapâ‚_mk (c : NumDenSameDeg ğ’œâ‚ ğ’®â‚) :
    HomogeneousLocalization.mapâ‚ g comap_le (mk c) =
    mk âŸ¨c.deg, âŸ¨_, g.2 c.num.2âŸ©, âŸ¨_, g.2 c.den.2âŸ©, comap_le c.den_memâŸ© := rfl

variable {fâ‚ : Aâ‚} {fâ‚‚ : Aâ‚‚} (hgf : g fâ‚ = fâ‚‚)

def Away.mapâ‚ : HomogeneousLocalization.Away ğ’œâ‚ fâ‚ â†’â‚[R] HomogeneousLocalization.Away ğ’œâ‚‚ fâ‚‚ :=
  HomogeneousLocalization.mapâ‚ g (Submonoid.powers_le.mpr âŸ¨1, by simp [hgf]âŸ©)

@[simp] lemma Away.mapâ‚_mk {d : Î¹} (hf : fâ‚ âˆˆ ğ’œâ‚ d) (n : â„•) (a : Aâ‚) (ha : a âˆˆ ğ’œâ‚ (n â€¢ d)) :
    mapâ‚ g hgf (.mk _ hf n a ha) = .mk _ (hgf â–¸ g.2 hf) n (g a) (g.2 ha) := by
  simp [mapâ‚, Away.mk, hgf]

@[simp] lemma Away.mapâ‚_of {d : Î¹} (hf : fâ‚ âˆˆ ğ’œâ‚ d) (n : â„•) (a : ğ’œâ‚ (n â€¢ d)) :
    mapâ‚ g hgf (of _ hf n a) = of _ (hgf â–¸ g.2 hf) n âŸ¨g a, g.2 a.2âŸ© :=
  mapâ‚_mk _ _ hf _ _ _

end GradedAlgHom

end HomogeneousLocalization
