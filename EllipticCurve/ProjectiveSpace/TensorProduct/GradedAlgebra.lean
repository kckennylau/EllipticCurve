/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

-- import Mathlib.LinearAlgebra.TensorProduct.Tower
import Mathlib.LinearAlgebra.DirectSum.TensorProduct
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.RingTheory.TensorProduct.Basic

/-! # Tensor Product of Graded Algebra

In this file we show that if `A` is a graded `R`-algebra then `S ‚äó[R] A` is a graded `S`-algebra.
-/

universe u v w

open DirectSum TensorProduct

@[simps!] def DirectSum.baseChangeLEquiv {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ]
    (M : Œπ ‚Üí Type*) [‚àÄ i, AddCommMonoid (M i)] [‚àÄ i, Module R (M i)]
    (S : Type*) [Semiring S] [Algebra R S] :
    S ‚äó[R] (‚®Å i, M i) ‚âÉ‚Çó[S] ‚®Å i, S ‚äó[R] M i where
  __ := directSumRight R S M
  map_smul' s x := by
    change directSumRight R S M (s ‚Ä¢ x) = s ‚Ä¢ directSumRight R S M x
    induction x with
    | zero => simp only [smul_zero, map_zero]
    | add x y hx hy => simp only [smul_add, map_add, hx, hy]
    | tmul s‚ÇÇ m => induction m using DirectSum.induction_on with
      | zero => simp only [tmul_zero, smul_zero, map_zero]
      | add m‚ÇÅ m‚ÇÇ hm‚ÇÅ hm‚ÇÇ => simp only [tmul_add, smul_add, map_add, hm‚ÇÅ, hm‚ÇÇ]
      | of i m => rw [‚Üê lof_eq_of R, smul_tmul', directSumRight_tmul_lof, directSumRight_tmul_lof,
          lof_eq_of, lof_eq_of, ‚Üê of_smul, smul_tmul']

theorem TensorProduct.congr_cast {R : Type*} [CommSemiring R] {Œπ : Type*}
    {M : Œπ ‚Üí Type*} [‚àÄ i, AddCommMonoid (M i)] [‚àÄ i, Module R (M i)]
    {S : Type*} [AddCommMonoid S] [Module R S]
    {i j : Œπ} (h : i = j) :
    congr (.refl R S) (.cast h) = .cast (M := fun i ‚Ü¶ S ‚äó[R] M i) h := by
  subst h
  exact LinearEquiv.toLinearMap_injective <| ext' fun x y ‚Ü¶ by simp

namespace GradedMonoid

theorem ext‚Çó (R : Type*) [CommSemiring R] {Œπ : Type*}
    {A : Œπ ‚Üí Type*} [‚àÄ i, AddCommMonoid (A i)] [‚àÄ i, Module R (A i)]
    {x y : GradedMonoid fun i ‚Ü¶ A i}
    (h‚ÇÅ : x.fst = y.fst) (h‚ÇÇ : LinearEquiv.cast (R := R) h‚ÇÅ x.snd = y.snd) : x = y := by
  obtain ‚ü®x‚ÇÅ, x‚ÇÇ‚ü© := x
  obtain ‚ü®y‚ÇÅ, y‚ÇÇ‚ü© := y
  dsimp only at h‚ÇÅ; subst h‚ÇÅ
  dsimp only at h‚ÇÇ; subst h‚ÇÇ
  rfl

@[simp] lemma gone {Œπ : Type*} [Zero Œπ] {A : Œπ ‚Üí Type*} [GOne A] :
    GOne.one (A := A) = 1 :=
  rfl

@[simp] lemma one_gmul {Œπ : Type*} [AddMonoid Œπ]
    {A : Œπ ‚Üí Type*} [GMonoid A] {i : Œπ} (x : A i) :
    GMul.mul (1 : A 0) x = cast (by rw [zero_add]) x :=
  eq_cast_iff_heq.mpr (Sigma.ext_iff.mp (GMonoid.one_mul ‚ü®_, x‚ü©)).2

@[simp] lemma gmul_one {Œπ : Type*} [AddMonoid Œπ]
    {A : Œπ ‚Üí Type*} [GMonoid A] {i : Œπ} (x : A i) :
    GMul.mul x (1 : A 0) = cast (by rw [add_zero]) x :=
  eq_cast_iff_heq.mpr (Sigma.ext_iff.mp (GMonoid.mul_one ‚ü®_, x‚ü©)).2

@[simp] lemma gmul_assoc {Œπ : Type*} [AddMonoid Œπ]
    {A : Œπ ‚Üí Type*} [GMonoid A] {i j k : Œπ} (x : A i) (y : A j) (z : A k) :
    GMul.mul (GMul.mul x y) z = cast (by rw [add_assoc]) (GMul.mul x (GMul.mul y z)) :=
  eq_cast_iff_heq.mpr (Sigma.ext_iff.mp (GMonoid.mul_assoc ‚ü®_, x‚ü© ‚ü®_, y‚ü© ‚ü®_, z‚ü©)).2

lemma gmul_comm {Œπ : Type*} [AddCommMonoid Œπ]
    {A : Œπ ‚Üí Type*} [GCommMonoid A] {i j : Œπ} (x : A i) (y : A j) :
    GMul.mul x y = cast (by rw [add_comm]) (GMul.mul y x) :=
  eq_cast_iff_heq.mpr (Sigma.ext_iff.mp (GCommMonoid.mul_comm ‚ü®_, x‚ü© ‚ü®_, y‚ü©)).2

instance GOne.tensorProduct {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ] [AddMonoid Œπ]
    (A : Œπ ‚Üí Type*) [‚àÄ i, AddCommMonoid (A i)] [‚àÄ i, Module R (A i)]
    [GSemiring A]
    (S : Type*) [AddCommMonoidWithOne S] [Module R S] :
    GOne fun i ‚Ü¶ S ‚äó[R] A i where
  one := 1

instance GMonoid.tensorProduct {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ] [AddMonoid Œπ]
    (A : Œπ ‚Üí Type*) [‚àÄ i, AddCommMonoid (A i)] [‚àÄ i, Module R (A i)]
    [GSemiring A] [GAlgebra R A]
    (S : Type*) [Semiring S] [Algebra R S] :
    GMonoid fun i ‚Ü¶ S ‚äó[R] A i where
  __ := GOne.tensorProduct A S
  mul x y := map‚ÇÇ (LinearMap.mul R S) (gMulLHom R A) x y
  one_mul x := by
    refine ext‚Çó R (by simp) ?_
    simp only [fst_mul, fst_one, snd_mul, snd_one, gone, Algebra.TensorProduct.one_def,
      map‚ÇÇ_apply_tmul]
    rw [‚Üê LinearEquiv.coe_toLinearMap, ‚Üê LinearMap.comp_apply]
    nth_rw 2 [‚Üê LinearMap.id_apply (R := R) x.snd]
    congr 1
    exact ext' (by simp_rw [‚Üê congr_cast]; simp)
  mul_one x := by
    refine ext‚Çó R (by simp) ?_
    simp only [fst_mul, fst_one, snd_mul, snd_one, gone, Algebra.TensorProduct.one_def]
    rw [‚Üê LinearMap.flip_apply, ‚Üê LinearEquiv.coe_toLinearMap, ‚Üê LinearMap.comp_apply]
    nth_rw 2 [‚Üê LinearMap.id_apply (R := R) x.snd]
    congr 1
    exact ext' (by simp_rw [‚Üê congr_cast]; simp)
  mul_assoc x y z := by
    refine ext‚Çó R (by simp [add_assoc]) ?_
    simp only [fst_mul, snd_mul]
    induction x.snd using TensorProduct.induction_on with
    | zero => simp [-LinearEquiv.cast_apply]
    | add x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => simp only [map_add, LinearMap.add_apply, hx‚ÇÅ, hx‚ÇÇ]
    | tmul s‚ÇÅ a =>
      induction y.snd using TensorProduct.induction_on with
      | zero => simp [-LinearEquiv.cast_apply]
      | add y‚ÇÅ y‚ÇÇ hy‚ÇÅ hy‚ÇÇ => simp only [map_add, LinearMap.add_apply, hy‚ÇÅ, hy‚ÇÇ]
      | tmul s‚ÇÇ b =>
        induction z.snd using TensorProduct.induction_on with
        | zero => simp [-LinearEquiv.cast_apply]
        | add z‚ÇÅ z‚ÇÇ hz‚ÇÅ hz‚ÇÇ => simp only [map_add, hz‚ÇÅ, hz‚ÇÇ]
        | tmul s‚ÇÉ c => rw [‚Üê congr_cast]; simp [_root_.mul_assoc]
  gnpow_zero' x := ext‚Çó R (by simp) (by simp [GradedMonoid.mk, gnpowRec])
  gnpow_succ' n x := ext‚Çó R (by simp) (by simp [GradedMonoid.mk, gnpowRec])

@[simp] lemma gmul_tensor_product {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ] [AddMonoid Œπ]
    (A : Œπ ‚Üí Type*) [‚àÄ i, AddCommMonoid (A i)] [‚àÄ i, Module R (A i)]
    [GSemiring A] [GAlgebra R A]
    (S : Type*) [Semiring S] [Algebra R S]
    {i j : Œπ} (x : S ‚äó[R] A i) (y : S ‚äó[R] A j) :
    GMul.mul (A := fun i ‚Ü¶ S ‚äó[R] A i) x y = map‚ÇÇ (LinearMap.mul R S) (gMulLHom R A) x y :=
  rfl

instance GCommMonoid.tensorProduct {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ] [AddCommMonoid Œπ]
    (A : Œπ ‚Üí Type*) [‚àÄ i, AddCommMonoid (A i)] [‚àÄ i, Module R (A i)]
    [GCommSemiring A] [GAlgebra R A]
    (S : Type*) [CommSemiring S] [Algebra R S] :
    GCommMonoid fun i ‚Ü¶ S ‚äó[R] A i where
  __ := GMonoid.tensorProduct A S
  mul_comm x y := by
    refine ext‚Çó R (by simp [add_comm]) ?_
    simp only [fst_mul, snd_mul, gmul_tensor_product]
    induction x.snd using TensorProduct.induction_on with
    | zero => simp [-LinearEquiv.cast_apply]
    | add x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => simp only [map_add, LinearMap.add_apply, hx‚ÇÅ, hx‚ÇÇ]
    | tmul s‚ÇÅ a =>
      induction y.snd using TensorProduct.induction_on with
      | zero => simp [-LinearEquiv.cast_apply]
      | add y‚ÇÅ y‚ÇÇ hy‚ÇÅ hy‚ÇÇ => simp only [map_add, LinearMap.add_apply, hy‚ÇÅ, hy‚ÇÇ]
      | tmul s‚ÇÇ b => rw [‚Üê congr_cast]; simp [gmul_comm a, _root_.mul_comm s‚ÇÅ]

end GradedMonoid

namespace DirectSum

instance GCommSemiring.tensorProduct {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ] [AddCommMonoid Œπ]
    (A : Œπ ‚Üí Type*) [‚àÄ i, AddCommMonoid (A i)] [‚àÄ i, Module R (A i)]
    [GCommSemiring A] [GAlgebra R A]
    (S : Type*) [CommSemiring S] [Algebra R S] :
    GCommSemiring fun i ‚Ü¶ S ‚äó[R] A i where
  __ := GradedMonoid.GCommMonoid.tensorProduct A S
  natCast n := n
  natCast_zero := by simp
  natCast_succ n := by simp
  mul_zero := by simp
  zero_mul := by simp
  mul_add := by simp
  add_mul := by simp

instance GAlgebra.tensorProduct {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ] [AddCommMonoid Œπ]
    (A : Œπ ‚Üí Type*) [‚àÄ i, AddCommMonoid (A i)] [‚àÄ i, Module R (A i)]
    [GCommSemiring A] [GAlgebra R A]
    (S : Type*) [CommSemiring S] [Algebra R S] :
    GAlgebra S fun i ‚Ü¶ S ‚äó[R] A i where
  toFun := (TensorProduct.mk R S (A 0)).flip 1
  map_one := rfl
  map_mul x y := GradedMonoid.ext‚Çó R (by simp [GradedMonoid.mk])
    (by rw [‚Üê congr_cast]; simp [GradedMonoid.mk])
  commutes s x := by
    refine GradedMonoid.ext‚Çó R (by simp [GradedMonoid.mk]) ?_
    simp_rw [‚Üê congr_cast, GradedMonoid.snd_mul, GradedMonoid.gmul_tensor_product, GradedMonoid.mk,
      GradedMonoid.fst_mul]
    rw [‚Üê LinearMap.flip_apply (m := x.snd), ‚Üê LinearEquiv.coe_toLinearMap, ‚Üê LinearMap.comp_apply]
    congr 1
    ext; simp [_root_.mul_comm]
  smul_def s x := by
    refine GradedMonoid.ext‚Çó R (by simp [GradedMonoid.mk]) ?_
    simp only [AddMonoidHom.coe_coe, LinearMap.flip_apply, mk_apply, GradedMonoid.fst_mul,
      GradedMonoid.fst_smul, GradedMonoid.snd_smul, GradedMonoid.snd_mul,
      GradedMonoid.gmul_tensor_product, GradedMonoid.mk]
    induction x.snd using TensorProduct.induction_on with
    | zero => simp [-LinearEquiv.cast_apply]
    | add x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => simp only [smul_add, map_add, hx‚ÇÅ, hx‚ÇÇ]
    | tmul r a => rw [‚Üê congr_cast]; simp [smul_tmul']

end DirectSum

def DirectSum.baseChangeAlgEquiv {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ] [AddCommMonoid Œπ]
    {A : Œπ ‚Üí Type*} [‚àÄ i, AddCommMonoid (A i)] [‚àÄ i, Module R (A i)]
    [GCommSemiring A] [GAlgebra R A]
    {S : Type*} [CommSemiring S] [Algebra R S] :
    S ‚äó[R] DirectSum Œπ A ‚âÉ‚Çê[S] ‚®Å i, S ‚äó[R] A i where
  __ := directSumRight R S A
  map_mul' x y := by
    dsimp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, LinearEquiv.coe_coe]
    induction x using TensorProduct.induction_on with
    | zero => simp
    | add x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => simp only [add_mul, map_add, hx‚ÇÅ, hx‚ÇÇ]
    | tmul s‚ÇÅ a‚ÇÅ =>
      induction y using TensorProduct.induction_on with
      | zero => simp
      | add y‚ÇÅ y‚ÇÇ hy‚ÇÅ hy‚ÇÇ => simp only [mul_add, map_add, hy‚ÇÅ, hy‚ÇÇ]
      | tmul s‚ÇÇ a‚ÇÇ =>
        induction a‚ÇÅ using DirectSum.induction_on with
        | zero => simp
        | add a‚ÇÅ b‚ÇÅ ha‚ÇÅ hb‚ÇÅ => simp only [tmul_add, add_mul, map_add, ha‚ÇÅ, hb‚ÇÅ]
        | of i a‚ÇÅ =>
          induction a‚ÇÇ using DirectSum.induction_on with
          | zero => simp
          | add a‚ÇÇ b‚ÇÇ ha‚ÇÇ hb‚ÇÇ => simp only [tmul_add, mul_add, map_add, ha‚ÇÇ, hb‚ÇÇ]
          | of j a‚ÇÇ => simp_rw [Algebra.TensorProduct.tmul_mul_tmul, of_mul_of,
              ‚Üê lof_eq_of R, directSumRight_tmul_lof, lof_eq_of, of_mul_of,
              GradedMonoid.gmul_tensor_product]; simp
  commutes' s := by
    dsimp only [Algebra.TensorProduct.algebraMap_apply, Algebra.algebraMap_self, RingHom.id_apply,
      AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, LinearEquiv.coe_coe]
    rw [DirectSum.one_def, ‚Üê lof_eq_of R, directSumRight_tmul_lof, algebraMap_apply]
    rfl

def DirectSum.lequivOfComponents {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ] [AddCommMonoid Œπ]
    {A‚ÇÅ : Œπ ‚Üí Type*} [‚àÄ i, AddCommMonoid (A‚ÇÅ i)] [‚àÄ i, Module R (A‚ÇÅ i)]
    {A‚ÇÇ : Œπ ‚Üí Type*} [‚àÄ i, AddCommMonoid (A‚ÇÇ i)] [‚àÄ i, Module R (A‚ÇÇ i)]
    (e : ‚àÄ i, A‚ÇÅ i ‚âÉ‚Çó[R] A‚ÇÇ i) :
    DirectSum Œπ A‚ÇÅ ‚âÉ‚Çó[R] DirectSum Œπ A‚ÇÇ :=
  .ofLinear (toModule _ _ _ (fun i ‚Ü¶ lof R Œπ A‚ÇÇ i ‚àò‚Çó e i))
    (toModule _ _ _ (fun i ‚Ü¶ lof R Œπ A‚ÇÅ i ‚àò‚Çó (e i).symm))
    (by ext; simp)
    (by ext; simp)

def DirectSum.algEquivOfComponents {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ] [AddCommMonoid Œπ]
    {A‚ÇÅ : Œπ ‚Üí Type*} [‚àÄ i, AddCommMonoid (A‚ÇÅ i)] [‚àÄ i, Module R (A‚ÇÅ i)]
    {A‚ÇÇ : Œπ ‚Üí Type*} [‚àÄ i, AddCommMonoid (A‚ÇÇ i)] [‚àÄ i, Module R (A‚ÇÇ i)]
    [GCommSemiring A‚ÇÅ] [GAlgebra R A‚ÇÅ]
    [GCommSemiring A‚ÇÇ] [GAlgebra R A‚ÇÇ]
    (e : ‚àÄ i, A‚ÇÅ i ‚âÉ‚Çó[R] A‚ÇÇ i)
    (one : e 0 1 = 1)
    (mul : ‚àÄ {i j} (x : A‚ÇÅ i) (y : A‚ÇÅ j), e (i + j) (GradedMonoid.GMul.mul x y) =
      GradedMonoid.GMul.mul (e i x) (e j y)) :
    DirectSum Œπ A‚ÇÅ ‚âÉ‚Çê[R] DirectSum Œπ A‚ÇÇ :=
  .ofAlgHom (toAlgebra _ _ (fun i ‚Ü¶ lof R Œπ A‚ÇÇ i ‚àò‚Çó e i) (by simp [one, lof_eq_of])
      fun xi xj ‚Ü¶ by simp [mul, lof_eq_of, of_mul_of])
    (toAlgebra _ _ (fun i ‚Ü¶ lof R Œπ A‚ÇÅ i ‚àò‚Çó (e i).symm) (by simp [‚Üê one, lof_eq_of])
      fun xi xj ‚Ü¶ by simp [lof_eq_of, of_mul_of, ‚Üê (e _).symm_apply_eq.mpr (mul _ _).symm])
    (by ext; simp [lof_eq_of])
    (by ext; simp [lof_eq_of])

def Submodule.toBaseChange {R : Type*} [CommSemiring R]
    {M : Type*} [AddCommMonoid M] [Module R M]
    (S : Type*) [Semiring S] [Algebra R S]
    (N : Submodule R M) : S ‚äó[R] N ‚Üí‚Çó[S] N.baseChange S :=
  LinearMap.rangeRestrict _

@[simp] lemma Submodule.toBaseChange_tmul_coe {R : Type*} [CommSemiring R]
    {M : Type*} [AddCommMonoid M] [Module R M]
    (S : Type*) [Semiring S] [Algebra R S]
    (N : Submodule R M) (s : S) (n : N) :
    Submodule.toBaseChange S N (s ‚äó‚Çú[R] n) = s ‚äó‚Çú[R] (n : M) :=
  rfl

lemma Submodule.toBaseChange_surjective {R : Type*} [CommSemiring R]
    {M : Type*} [AddCommMonoid M] [Module R M]
    (S : Type*) [Semiring S] [Algebra R S]
    (N : Submodule R M) :
    Function.Surjective (Submodule.toBaseChange S N) :=
  LinearMap.surjective_rangeRestrict _

lemma Function.Bijective.of_comp_of_surjective {Œ± Œ≤ Œ≥ : Sort*} {f : Œ≤ ‚Üí Œ≥} {g : Œ± ‚Üí Œ≤}
    (hfg : Function.Bijective (f ‚àò g)) (hg : Function.Surjective g) : Function.Bijective f :=
  ‚ü®.of_comp_right hfg.injective hg, .of_comp hfg.surjective‚ü©

private theorem DirectSum.IsInternal.baseChange_aux {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ]
    {A : Type*} [AddCommMonoid A] [Module R A]
    {ùíú : Œπ ‚Üí Submodule R A} (internal : IsInternal ùíú)
    (S : Type*) [Semiring S] [Algebra R S] :
    (IsInternal fun i ‚Ü¶ (ùíú i).baseChange S) ‚àß ‚àÄ i, Function.Injective ((ùíú i).toBaseChange S) := by
  have := internal.chooseDecomposition
  let e := (baseChangeLEquiv (R := R) (fun i ‚Ü¶ ùíú i) S).symm ‚â™‚â´‚Çó
    (decomposeLinearEquiv ùíú).symm.baseChange R S
  let e‚ÇÅ : (‚®Å i, S ‚äó[R] ùíú i) ‚Üí‚Çó[S] ‚®Å i, (ùíú i).baseChange S :=
    lmap fun i ‚Ü¶ (ùíú i).toBaseChange S
  let e‚ÇÇ : (‚®Å i, (ùíú i).baseChange S) ‚Üí‚Çó[S] S ‚äó[R] A :=
    toModule _ _ _ fun i ‚Ü¶ Submodule.subtype _
  have he : e‚ÇÇ ‚àò‚Çó e‚ÇÅ = e := by
    ext
    simp only [AlgebraTensorModule.curry_apply, curry_apply,
      LinearMap.coe_comp, LinearMap.coe_restrictScalars, Function.comp_apply, lmap_lof,
      toModule_lof, Submodule.subtype_apply, Submodule.toBaseChange_tmul_coe, LinearEquiv.coe_coe,
      LinearEquiv.trans_apply, baseChangeLEquiv_symm_apply, e‚ÇÇ, e‚ÇÅ, e]
    rw [lof_eq_of S, ‚Üê lof_eq_of R, directSumRight_symm_lof_tmul]
    simp [LinearEquiv.baseChange, lof_eq_of]
  have he' : e‚ÇÇ ‚àò e‚ÇÅ = e := congr($he)
  have he‚ÇÇ : Function.Bijective e‚ÇÇ :=
    .of_comp_of_surjective (g := e‚ÇÅ) (he' ‚ñ∏ e.bijective)
      ((lmap_surjective _).mpr fun _ ‚Ü¶ LinearMap.surjective_rangeRestrict _)
  have he‚ÇÅ : Function.Injective e‚ÇÅ := .of_comp (f := e‚ÇÇ) (he' ‚ñ∏ e.injective)
  exact ‚ü®he‚ÇÇ, fun i x y h ‚Ü¶ of_injective (Œ≤ := fun i ‚Ü¶ S ‚äó[R] ùíú i) i <| he‚ÇÅ <| by simp [e‚ÇÅ, h]‚ü©

theorem DirectSum.IsInternal.baseChange {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ]
    {A : Type*} [AddCommMonoid A] [Module R A]
    {ùíú : Œπ ‚Üí Submodule R A} (internal : IsInternal ùíú)
    (S : Type*) [Semiring S] [Algebra R S] :
    IsInternal fun i ‚Ü¶ (ùíú i).baseChange S :=
  (internal.baseChange_aux S).1

theorem DirectSum.IsInternal.toBaseChange_bijective {R : Type*} [CommSemiring R]
    {Œπ : Type*} [DecidableEq Œπ]
    {A : Type*} [AddCommMonoid A] [Module R A]
    {ùíú : Œπ ‚Üí Submodule R A} (internal : IsInternal ùíú)
    (S : Type*) [Semiring S] [Algebra R S] (i : Œπ) :
    Function.Bijective (Submodule.toBaseChange S (ùíú i)) :=
  ‚ü®(internal.baseChange_aux S).2 i, (ùíú i).toBaseChange_surjective S‚ü©

variable {R A : Type*} [CommRing R] [CommRing A] [Algebra R A]
  {Œπ : Type*} [DecidableEq Œπ] [AddCommMonoid Œπ]
  (ùíú : Œπ ‚Üí Submodule R A) [GradedAlgebra ùíú]
  (S : Type*) [CommRing S] [Algebra R S]

namespace GradedAlgebra

open TensorProduct Submodule DirectSum

instance : SetLike.GradedMonoid (fun n ‚Ü¶ (ùíú n).baseChange S) where
  one_mem := by
    rw [Algebra.TensorProduct.one_def]
    exact tmul_mem_baseChange_of_mem _ (SetLike.one_mem_graded _)
  mul_mem {i j} := by
    suffices h : (ùíú i).baseChange S * (ùíú j).baseChange S ‚â§ (ùíú (i + j)).baseChange S from
      fun _ _ hx hy ‚Ü¶ h (mul_mem_mul hx hy)
    rw [baseChange_eq_span, baseChange_eq_span, span_mul_span, span_le, Set.mul_subset_iff]
    rintro - ‚ü®x, hx, rfl‚ü© - ‚ü®y, hy, rfl‚ü©
    simp only [mk_apply, Algebra.TensorProduct.tmul_mul_tmul, mul_one]
    exact tmul_mem_baseChange_of_mem _ (SetLike.mul_mem_graded hx hy)

-- def tensorProductAlgEquiv : S ‚äó[R] A ‚âÉ‚Çê[S] ‚®Å i, (ùíú i).baseChange S :=
--   (Algebra.TensorProduct.congr AlgEquiv.refl (DirectSum.decomposeAlgEquiv ùíú)).trans <|
--     (baseChangeAlgEquiv (R := R) (S := S) (A := fun i ‚Ü¶ ùíú i)).trans <|
--       algEquivOfComponents (fun i ‚Ü¶ .ofBijective ((ùíú i).toBaseChange S) _)  _  _

noncomputable def baseChangeLEquiv (n : Œπ) : S ‚äó[R] ùíú n ‚âÉ‚Çó[S] (ùíú n).baseChange S :=
  LinearEquiv.ofBijective _ ((Decomposition.isInternal ùíú).toBaseChange_bijective S n)

noncomputable instance : GradedAlgebra (fun n ‚Ü¶ (ùíú n).baseChange S) :=
  ((Decomposition.isInternal ùíú).baseChange S).gradedAlgebra

/- where
  one_mem := by
    rw [Algebra.TensorProduct.one_def]
    exact tmul_mem_baseChange_of_mem _ (SetLike.one_mem_graded _)
  mul_mem {i j} := by
    suffices h : (ùíú i).baseChange S * (ùíú j).baseChange S ‚â§ (ùíú (i + j)).baseChange S from
      fun _ _ hx hy ‚Ü¶ h (mul_mem_mul hx hy)
    rw [baseChange_eq_span, baseChange_eq_span, span_mul_span, span_le, Set.mul_subset_iff]
    rintro - ‚ü®x, hx, rfl‚ü© - ‚ü®y, hy, rfl‚ü©
    simp only [mk_apply, Algebra.TensorProduct.tmul_mul_tmul, mul_one]
    exact tmul_mem_baseChange_of_mem _ (SetLike.mul_mem_graded hx hy)
  decompose' := _ -/

end GradedAlgebra
