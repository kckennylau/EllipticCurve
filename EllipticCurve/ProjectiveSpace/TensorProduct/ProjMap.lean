/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import EllipticCurve.ProjectiveSpace.AlgebraHomogeneousLocalization
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.Basic

/-! # Functoriality of Proj
-/

-- not `@[ext]` because `A` cannot be inferred.
theorem IsLocalization.algHom_ext {R A L B : Type*}
    [CommSemiring R] [CommSemiring A] [CommSemiring L] [CommSemiring B]
    (W : Submonoid A) [Algebra A L] [IsLocalization W L]
    [Algebra R A] [Algebra R L] [IsScalarTower R A L] [Algebra R B]
    {f g : L â†’â‚[R] B} (h : f.comp (Algebra.algHom R A L) = g.comp (Algebra.algHom R A L)) :
    f = g :=
  AlgHom.coe_ringHom_injective <| IsLocalization.ringHom_ext W <| RingHom.ext <| AlgHom.ext_iff.mp h

@[ext high] theorem Localization.algHom_ext {R A B : Type*}
    [CommSemiring R] [CommSemiring A] [CommSemiring B] [Algebra R A] [Algebra R B] (W : Submonoid A)
    {f g : Localization W â†’â‚[R] B}
    (h : f.comp (Algebra.algHom R A _) = g.comp (Algebra.algHom R A _)) :
    f = g :=
  IsLocalization.algHom_ext W h

universe u v

@[simp] lemma Localization.localRingHom_mk {R P : Type*} [CommSemiring R] [CommSemiring P]
    (I : Ideal R) [I.IsPrime] (J : Ideal P) [J.IsPrime] (f : R â†’+* P) (hIJ : I = Ideal.comap f J)
    (x : R) (y : I.primeCompl) :
    Localization.localRingHom I J f hIJ (Localization.mk x y) =
      Localization.mk (f x) âŸ¨f y, by aesopâŸ© := by
  simp [mk_eq_mk', localRingHom_mk']

lemma HomogeneousIdeal.toIdeal_le_toIdeal_iff {Î¹ Ïƒ A : Type*} [Semiring A] [SetLike Ïƒ A]
    [AddSubmonoidClass Ïƒ A] (ğ’œ : Î¹ â†’ Ïƒ) [DecidableEq Î¹] [AddMonoid Î¹] [GradedRing ğ’œ]
    {I J : HomogeneousIdeal ğ’œ} : I.toIdeal â‰¤ J.toIdeal â†” I â‰¤ J := Iff.rfl

variable {R : Type u} {Aâ‚ Aâ‚‚ : Type v} [CommRing R] [CommRing Aâ‚] [CommRing Aâ‚‚]
  [Algebra R Aâ‚] [Algebra R Aâ‚‚]
  (ğ’œâ‚ : â„• â†’ Submodule R Aâ‚) (ğ’œâ‚‚ : â„• â†’ Submodule R Aâ‚‚)

structure GradedAlgHom extends Aâ‚ â†’â‚[R] Aâ‚‚ where
  map_mem' : âˆ€ â¦ƒn aâ¦„, a âˆˆ ğ’œâ‚ n â†’ toAlgHom a âˆˆ ğ’œâ‚‚ n

@[inherit_doc]
notation:25 ğ’œâ‚ " â†’áµáµƒ " ğ’œâ‚‚ => GradedAlgHom ğ’œâ‚ ğ’œâ‚‚

namespace GradedAlgHom

variable {ğ’œâ‚ ğ’œâ‚‚}

theorem toAlgHom_injective : Function.Injective (toAlgHom : (ğ’œâ‚ â†’áµáµƒ ğ’œâ‚‚) â†’ (Aâ‚ â†’â‚[R] Aâ‚‚)) := by
  rintro âŸ¨_âŸ© _ h
  congr

instance funLike : FunLike (ğ’œâ‚ â†’áµáµƒ ğ’œâ‚‚) Aâ‚ Aâ‚‚ where
  coe f := f.toFun
  coe_injective' _ _ h := toAlgHom_injective <| AlgHom.coe_fn_injective h

instance algHomClass : AlgHomClass (ğ’œâ‚ â†’áµáµƒ ğ’œâ‚‚) R Aâ‚ Aâ‚‚ where
  map_mul f := f.map_mul
  map_add f := f.map_add
  map_one f := f.map_one
  map_zero f := f.map_zero
  commutes f := f.commutes

variable (f : ğ’œâ‚ â†’áµáµƒ ğ’œâ‚‚)

@[simp] lemma coe_toAlgHom : (f.toAlgHom : Aâ‚ â†’ Aâ‚‚) = f := rfl

lemma map_mem {n : â„•} {a : Aâ‚} (ha : a âˆˆ ğ’œâ‚ n) : f a âˆˆ ğ’œâ‚‚ n :=
  f.map_mem' ha

@[simps] def linearMap (n : â„•) : ğ’œâ‚ n â†’â‚—[R] ğ’œâ‚‚ n where
  toFun a := âŸ¨f a, f.map_mem a.2âŸ©
  map_add' x y := by ext; simp
  map_smul' c x := by ext; simp

@[simp] lemma decompose_map [GradedAlgebra ğ’œâ‚] [GradedAlgebra ğ’œâ‚‚] {x : Aâ‚} :
    DirectSum.decompose ğ’œâ‚‚ (f x) = .map (fun n â†¦ f.linearMap n) (.decompose ğ’œâ‚ x) := by
  classical
  rw [â† DirectSum.sum_support_decompose ğ’œâ‚ x, map_sum, DirectSum.decompose_sum,
    DirectSum.decompose_sum, map_sum]
  congr 1
  ext n : 1
  rw [DirectSum.decompose_of_mem _ (f.map_mem (Subtype.prop _)),
    DirectSum.decompose_of_mem _ (Subtype.prop _), DirectSum.map_of]
  rfl

lemma map_coe_decompose [GradedAlgebra ğ’œâ‚] [GradedAlgebra ğ’œâ‚‚] {x : Aâ‚} {n : â„•} :
    f (DirectSum.decompose ğ’œâ‚ x n) = DirectSum.decompose ğ’œâ‚‚ (f x) n := by
  simp

end GradedAlgHom

namespace HomogeneousIdeal

variable {ğ’œâ‚ ğ’œâ‚‚} [GradedAlgebra ğ’œâ‚] [GradedAlgebra ğ’œâ‚‚] (f : ğ’œâ‚ â†’áµáµƒ ğ’œâ‚‚)

@[simp] lemma coe_toIdeal (I : HomogeneousIdeal ğ’œâ‚) : (I.toIdeal : Set Aâ‚) = I := rfl

def map (I : HomogeneousIdeal ğ’œâ‚) : HomogeneousIdeal ğ’œâ‚‚ where
  __ := I.toIdeal.map f
  is_homogeneous' n a ha := by
    rw [Ideal.map] at ha
    induction ha using Submodule.span_induction generalizing n with
    | mem a ha =>
      obtain âŸ¨a, ha, rflâŸ© := ha
      rw [â† f.map_coe_decompose]
      exact Ideal.mem_map_of_mem _ (I.2 _ ha)
    | zero => simp
    | add => simp [*, Ideal.add_mem]
    | smul aâ‚ aâ‚‚ haâ‚‚ ih =>
      classical rw [smul_eq_mul, DirectSum.decompose_mul, DirectSum.coe_mul_apply]
      exact sum_mem fun ij hij â†¦ Ideal.mul_mem_left _ _ <| ih _

def comap (I : HomogeneousIdeal ğ’œâ‚‚) : HomogeneousIdeal ğ’œâ‚ where
  __ := I.toIdeal.comap f
  is_homogeneous' n a ha := by
    rw [Ideal.mem_comap, HomogeneousIdeal.mem_iff, f.map_coe_decompose]
    exact I.2 _ ha

variable {f}

lemma coe_comap (I : HomogeneousIdeal ğ’œâ‚‚) : (I.comap f : Set Aâ‚) = f â»Â¹' I := rfl

lemma map_le_iff_le_comap {I : HomogeneousIdeal ğ’œâ‚} {J : HomogeneousIdeal ğ’œâ‚‚} :
    I.map f â‰¤ J â†” I â‰¤ J.comap f :=
  Ideal.map_le_iff_le_comap
alias âŸ¨le_comap_of_map_le, map_le_of_le_comapâŸ© := map_le_iff_le_comap

theorem gc_map_comap : GaloisConnection (map f) (comap f) := fun _ _ â†¦
  map_le_iff_le_comap

end HomogeneousIdeal

namespace HomogeneousLocalization

variable {ğ’œâ‚ ğ’œâ‚‚} [GradedAlgebra ğ’œâ‚] [GradedAlgebra ğ’œâ‚‚] (f : ğ’œâ‚ â†’áµáµƒ ğ’œâ‚‚)

noncomputable def localRingHom (I : Ideal Aâ‚) [I.IsPrime] (J : Ideal Aâ‚‚) [J.IsPrime]
    (hIJ : I = J.comap f) :
    AtPrime ğ’œâ‚ I â†’+* AtPrime ğ’œâ‚‚ J :=
  map _ _ f (Localization.le_comap_primeCompl_iff.mpr <| hIJ â–¸ le_rfl) f.2

variable (I : Ideal Aâ‚) [I.IsPrime] (J : Ideal Aâ‚‚) [J.IsPrime] (hIJ : I = J.comap f)

@[simp] lemma val_localRingHom (x : AtPrime ğ’œâ‚ I) :
    (localRingHom f I J hIJ x).val = Localization.localRingHom _ _ f hIJ x.val := by
  obtain âŸ¨âŸ¨i, x, s, hsâŸ©, rflâŸ© := x.mk_surjective
  simp [localRingHom, map_mk]

instance isLocalHom_localRingHom : IsLocalHom (localRingHom f I J hIJ) where
  map_nonunit x hx := by
    rw [â† isUnit_iff_isUnit_val] at hx âŠ¢
    rw [val_localRingHom] at hx
    exact IsLocalHom.map_nonunit _ hx

@[simps] def NumDenSameDeg.map {Wâ‚ : Submonoid Aâ‚} {Wâ‚‚ : Submonoid Aâ‚‚}
    (hw : Wâ‚ â‰¤ Wâ‚‚.comap f) (c : NumDenSameDeg ğ’œâ‚ Wâ‚) : NumDenSameDeg ğ’œâ‚‚ Wâ‚‚ where
  deg := c.deg
  den := f.linearMap _ c.den
  num := f.linearMap _ c.num
  den_mem := hw c.den_mem

lemma localRingHom_mk (c : NumDenSameDeg ğ’œâ‚ I.primeCompl) :
    localRingHom f I J hIJ (.mk c) =
      .mk (c.map f <| hIJ â–¸ by rfl) := by
  rfl

def lof {R A : Type*} [CommRing R] [CommRing A] [Algebra R A]
    {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    (S : Submonoid A) {i : Î¹} (d : ğ’œ i) (hd : â†‘d âˆˆ S) :
    ğ’œ i â†’â‚—[R] HomogeneousLocalization ğ’œ S where
  toFun x := mk âŸ¨i, x, d, hdâŸ©
  map_add' x y := by ext; simp [Localization.add_mk_self]
  map_smul' c x := by ext; simp [Localization.smul_mk]

nonrec def Away.lof {R A : Type*} [CommRing R] [CommRing A] [Algebra R A]
    {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    {i : Î¹} {f : A} (hf : f âˆˆ ğ’œ i) (n : â„•) :
    ğ’œ (n â€¢ i) â†’â‚—[R] HomogeneousLocalization.Away ğ’œ f :=
  lof _ _ âŸ¨f ^ n, SetLike.pow_mem_graded _ hfâŸ© âŸ¨n, rflâŸ©

@[simp] lemma Away.val_lof
    {R A : Type*} [CommRing R] [CommRing A] [Algebra R A]
    {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    {i : Î¹} {f : A} (hf : f âˆˆ ğ’œ i) (n : â„•) (a : ğ’œ (n â€¢ i)) :
    (lof _ hf n a).val = .mk a âŸ¨f ^ n, n, rflâŸ© := rfl

lemma Away.lof_surjective
    {R A : Type*} [CommRing R] [CommRing A] [Algebra R A]
    {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    {i : Î¹} {f : A} (hf : f âˆˆ ğ’œ i) (x : Away ğ’œ f) :
    âˆƒ (n : â„•) (a : ğ’œ (n â€¢ i)), lof _ hf n a = x := by
  obtain âŸ¨n, a, ha, rflâŸ© := x.mk_surjective _ hf
  exact âŸ¨n, âŸ¨a, haâŸ©, rflâŸ©

open NumDenSameDeg in
def mapâ‚ {R Râ‚ Râ‚‚ Aâ‚ Aâ‚‚ : Type*}
    [CommRing R] [CommRing Râ‚] [CommRing Râ‚‚] [CommRing Aâ‚] [CommRing Aâ‚‚]
    [Algebra R Râ‚] [Algebra Râ‚ Aâ‚] [Algebra R Aâ‚] [IsScalarTower R Râ‚ Aâ‚]
    [Algebra R Râ‚‚] [Algebra Râ‚‚ Aâ‚‚] [Algebra R Aâ‚‚] [IsScalarTower R Râ‚‚ Aâ‚‚]
    {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹]
    {ğ’œâ‚ : Î¹ â†’ Submodule Râ‚ Aâ‚} [GradedAlgebra ğ’œâ‚]
    {ğ’œâ‚‚ : Î¹ â†’ Submodule Râ‚‚ Aâ‚‚} [GradedAlgebra ğ’œâ‚‚]
    {ğ’®â‚ : Submonoid Aâ‚} {ğ’®â‚‚ : Submonoid Aâ‚‚}
    (g : Aâ‚ â†’â‚[R] Aâ‚‚) (comap_le : ğ’®â‚ â‰¤ Submonoid.comap g ğ’®â‚‚)
    (hg : âˆ€ â¦ƒiâ¦„, âˆ€ a âˆˆ ğ’œâ‚ i, g a âˆˆ ğ’œâ‚‚ i) :
    HomogeneousLocalization ğ’œâ‚ ğ’®â‚ â†’â‚[R] HomogeneousLocalization ğ’œâ‚‚ ğ’®â‚‚ where
  toFun := Quotient.map'
    (fun x â†¦ âŸ¨x.deg, âŸ¨_, hg _ x.num.2âŸ©, âŸ¨_, hg _ x.den.2âŸ©, comap_le x.den_memâŸ©)
    fun x y (e : x.embedding = y.embedding) â†¦ by
      apply_fun IsLocalization.map (Localization ğ’®â‚‚) (g : Aâ‚ â†’+* Aâ‚‚) comap_le at e
      simp_rw [HomogeneousLocalization.NumDenSameDeg.embedding, Localization.mk_eq_mk',
        IsLocalization.map_mk', â† Localization.mk_eq_mk'] at e
      exact e
  map_add' := Quotient.indâ‚‚' fun x y â†¦ by
    simp only [â† mk_add, Quotient.map'_mk'', num_add, map_add, map_mul, den_add]; rfl
  map_mul' := Quotient.indâ‚‚' fun x y â†¦ by
    simp only [â† mk_mul, Quotient.map'_mk'', num_mul, map_mul, den_mul]; rfl
  map_zero' := by simp only [â† mk_zero (ğ’œ := ğ’œâ‚), Quotient.map'_mk'', deg_zero,
    num_zero, ZeroMemClass.coe_zero, map_zero, den_zero, map_one]; rfl
  map_one' := by simp only [â† mk_one (ğ’œ := ğ’œâ‚), Quotient.map'_mk'',
    num_one, den_one, map_one]; rfl
  commutes' r := by ext; simp [fromZeroRingHom]

@[simp] lemma mapâ‚_mk {R Râ‚ Râ‚‚ Aâ‚ Aâ‚‚ : Type*}
    [CommRing R] [CommRing Râ‚] [CommRing Râ‚‚] [CommRing Aâ‚] [CommRing Aâ‚‚]
    [Algebra R Râ‚] [Algebra Râ‚ Aâ‚] [Algebra R Aâ‚] [IsScalarTower R Râ‚ Aâ‚]
    [Algebra R Râ‚‚] [Algebra Râ‚‚ Aâ‚‚] [Algebra R Aâ‚‚] [IsScalarTower R Râ‚‚ Aâ‚‚]
    {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹]
    {ğ’œâ‚ : Î¹ â†’ Submodule Râ‚ Aâ‚} [GradedAlgebra ğ’œâ‚]
    {ğ’œâ‚‚ : Î¹ â†’ Submodule Râ‚‚ Aâ‚‚} [GradedAlgebra ğ’œâ‚‚]
    {ğ’®â‚ : Submonoid Aâ‚} {ğ’®â‚‚ : Submonoid Aâ‚‚}
    (g : Aâ‚ â†’â‚[R] Aâ‚‚) (comap_le : ğ’®â‚ â‰¤ Submonoid.comap g ğ’®â‚‚)
    (hg : âˆ€ â¦ƒiâ¦„, âˆ€ a âˆˆ ğ’œâ‚ i, g a âˆˆ ğ’œâ‚‚ i)
    (c : NumDenSameDeg ğ’œâ‚ ğ’®â‚) :
    HomogeneousLocalization.mapâ‚ g comap_le hg (mk c) =
    mk âŸ¨c.deg, âŸ¨_, hg _ c.num.2âŸ©, âŸ¨_, hg _ c.den.2âŸ©, comap_le c.den_memâŸ© := rfl

def Away.mapâ‚ {R Râ‚ Râ‚‚ Aâ‚ Aâ‚‚ : Type*}
    [CommRing R] [CommRing Râ‚] [CommRing Râ‚‚] [CommRing Aâ‚] [CommRing Aâ‚‚]
    [Algebra R Râ‚] [Algebra Râ‚ Aâ‚] [Algebra R Aâ‚] [IsScalarTower R Râ‚ Aâ‚]
    [Algebra R Râ‚‚] [Algebra Râ‚‚ Aâ‚‚] [Algebra R Aâ‚‚] [IsScalarTower R Râ‚‚ Aâ‚‚]
    {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹]
    {ğ’œâ‚ : Î¹ â†’ Submodule Râ‚ Aâ‚} [GradedAlgebra ğ’œâ‚]
    {ğ’œâ‚‚ : Î¹ â†’ Submodule Râ‚‚ Aâ‚‚} [GradedAlgebra ğ’œâ‚‚]
    {fâ‚ : Aâ‚} {fâ‚‚ : Aâ‚‚} (g : Aâ‚ â†’â‚[R] Aâ‚‚) (hg : âˆ€ â¦ƒiâ¦„, âˆ€ a âˆˆ ğ’œâ‚ i, g a âˆˆ ğ’œâ‚‚ i)
    (hgf : g fâ‚ = fâ‚‚) :
    HomogeneousLocalization.Away ğ’œâ‚ fâ‚ â†’â‚[R] HomogeneousLocalization.Away ğ’œâ‚‚ fâ‚‚ :=
  HomogeneousLocalization.mapâ‚ g (Submonoid.powers_le.mpr âŸ¨1, by simp [hgf]âŸ©) hg

@[simp] lemma Away.mapâ‚_mk {R Râ‚ Râ‚‚ Aâ‚ Aâ‚‚ : Type*}
    [CommRing R] [CommRing Râ‚] [CommRing Râ‚‚] [CommRing Aâ‚] [CommRing Aâ‚‚]
    [Algebra R Râ‚] [Algebra Râ‚ Aâ‚] [Algebra R Aâ‚] [IsScalarTower R Râ‚ Aâ‚]
    [Algebra R Râ‚‚] [Algebra Râ‚‚ Aâ‚‚] [Algebra R Aâ‚‚] [IsScalarTower R Râ‚‚ Aâ‚‚]
    {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹]
    {ğ’œâ‚ : Î¹ â†’ Submodule Râ‚ Aâ‚} [GradedAlgebra ğ’œâ‚]
    {ğ’œâ‚‚ : Î¹ â†’ Submodule Râ‚‚ Aâ‚‚} [GradedAlgebra ğ’œâ‚‚]
    {fâ‚ : Aâ‚} {fâ‚‚ : Aâ‚‚} (g : Aâ‚ â†’â‚[R] Aâ‚‚) (hg : âˆ€ â¦ƒiâ¦„, âˆ€ a âˆˆ ğ’œâ‚ i, g a âˆˆ ğ’œâ‚‚ i)
    (hgf : g fâ‚ = fâ‚‚) {d : Î¹} (hf : fâ‚ âˆˆ ğ’œâ‚ d) (n : â„•) (a : Aâ‚) (ha : a âˆˆ ğ’œâ‚ (n â€¢ d)) :
    mapâ‚ g hg hgf (.mk _ hf n a ha) = .mk _ (hgf â–¸ hg _ hf) n (g a) (hg _ ha) := by
  simp [mapâ‚, Away.mk, hgf]

@[simp] lemma Away.mapâ‚_lof {R Râ‚ Râ‚‚ Aâ‚ Aâ‚‚ : Type*}
    [CommRing R] [CommRing Râ‚] [CommRing Râ‚‚] [CommRing Aâ‚] [CommRing Aâ‚‚]
    [Algebra R Râ‚] [Algebra Râ‚ Aâ‚] [Algebra R Aâ‚] [IsScalarTower R Râ‚ Aâ‚]
    [Algebra R Râ‚‚] [Algebra Râ‚‚ Aâ‚‚] [Algebra R Aâ‚‚] [IsScalarTower R Râ‚‚ Aâ‚‚]
    {Î¹ : Type*} [DecidableEq Î¹] [AddCommMonoid Î¹]
    {ğ’œâ‚ : Î¹ â†’ Submodule Râ‚ Aâ‚} [GradedAlgebra ğ’œâ‚]
    {ğ’œâ‚‚ : Î¹ â†’ Submodule Râ‚‚ Aâ‚‚} [GradedAlgebra ğ’œâ‚‚]
    {d : Î¹} {fâ‚ : Aâ‚} (hf : fâ‚ âˆˆ ğ’œâ‚ d) {fâ‚‚ : Aâ‚‚}
    (g : Aâ‚ â†’â‚[R] Aâ‚‚) (hg : âˆ€ â¦ƒiâ¦„, âˆ€ a âˆˆ ğ’œâ‚ i, g a âˆˆ ğ’œâ‚‚ i)
    (hgf : g fâ‚ = fâ‚‚) (n : â„•) (a : ğ’œâ‚ (n â€¢ d)) :
    mapâ‚ g hg hgf (lof _ hf n a) = lof _ (hgf â–¸ hg _ hf) n âŸ¨g a, hg _ a.2âŸ© :=
  mapâ‚_mk _ _ _ hf _ _ _

end HomogeneousLocalization


variable {ğ’œâ‚ ğ’œâ‚‚} [GradedAlgebra ğ’œâ‚] [GradedAlgebra ğ’œâ‚‚] (f : ğ’œâ‚ â†’áµáµƒ ğ’œâ‚‚)
  (hf : HomogeneousIdeal.irrelevant ğ’œâ‚‚ â‰¤ (HomogeneousIdeal.irrelevant ğ’œâ‚).map f)

namespace ProjectiveSpectrum

@[simps] def comap.toFun (p : ProjectiveSpectrum ğ’œâ‚‚) : ProjectiveSpectrum ğ’œâ‚ where
  asHomogeneousIdeal := p.1.comap f
  isPrime := p.2.comap f
  not_irrelevant_le le := p.3 <| hf.trans <| HomogeneousIdeal.map_le_of_le_comap le

def comap : C(ProjectiveSpectrum ğ’œâ‚‚, ProjectiveSpectrum ğ’œâ‚) where
  toFun := comap.toFun f hf
  continuous_toFun := by
    simp only [continuous_iff_isClosed, isClosed_iff_zeroLocus]
    rintro _ âŸ¨s, rflâŸ©
    refine âŸ¨f '' s, ?_âŸ©
    ext x
    simp only [mem_zeroLocus, Set.image_subset_iff, Set.mem_preimage, mem_zeroLocus,
      comap.toFun_asHomogeneousIdeal, HomogeneousIdeal.coe_comap]

end ProjectiveSpectrum

namespace AlgebraicGeometry.Proj

open TopologicalSpace ProjectiveSpectrum Opposite HomogeneousLocalization

namespace StructureSheaf

variable (U : Opens (ProjectiveSpectrum ğ’œâ‚)) (V : Opens (ProjectiveSpectrum ğ’œâ‚‚))
  (hUV : V.1 âŠ† ProjectiveSpectrum.comap f hf â»Â¹' U.1)

noncomputable def comapFun (s : âˆ€ x : U, AtPrime ğ’œâ‚ x.1.1.1) (y : V) :
    AtPrime ğ’œâ‚‚ y.1.1.1 :=
  localRingHom f _ y.1.1.1 rfl <| s âŸ¨.comap f hf y.1, hUV y.2âŸ©

lemma isLocallyFraction_comapFun
    (s : âˆ€ x : U, AtPrime ğ’œâ‚ x.1.1.1)
    (hs : (ProjectiveSpectrum.StructureSheaf.isLocallyFraction ğ’œâ‚).pred s) :
    (ProjectiveSpectrum.StructureSheaf.isLocallyFraction ğ’œâ‚‚).pred
      (comapFun f hf U (unop (op V)) hUV â†‘s) := by
  rintro âŸ¨p, hpVâŸ©
  rcases hs âŸ¨.comap f hf p, hUV hpVâŸ© with âŸ¨W, m, iWU, i, a, b, hb, h_fracâŸ©
  refine âŸ¨W.comap (ProjectiveSpectrum.comap f hf) âŠ“ V, âŸ¨m, hpVâŸ©, Opens.infLERight _ _, i,
    f.linearMap i a, f.linearMap i b, fun âŸ¨q, âŸ¨hqW, hqVâŸ©âŸ© â†¦ hb âŸ¨_, hqWâŸ©, ?_âŸ©
  rintro âŸ¨q, âŸ¨hqW, hqVâŸ©âŸ©
  ext
  specialize h_frac âŸ¨_, hqWâŸ©
  simp_all [comapFun]

noncomputable def comap :
    (Proj.structureSheaf ğ’œâ‚).1.obj (op U) â†’+* (Proj.structureSheaf ğ’œâ‚‚).1.obj (op V) where
  toFun s := âŸ¨comapFun _ _ _ _ hUV s.1, isLocallyFraction_comapFun _ _ _ _ hUV _ s.2âŸ©
  map_one' := by ext; simp [comapFun]
  map_zero' := by ext; simp [comapFun]
  map_add' x y := by ext; simp [comapFun]
  map_mul' x y := by ext; simp [comapFun]

end StructureSheaf

open CategoryTheory

@[simps (isSimp := false)] noncomputable def sheafedSpaceMap :
    Proj.toSheafedSpace ğ’œâ‚‚ âŸ¶ Proj.toSheafedSpace ğ’œâ‚ where
  base := TopCat.ofHom <| ProjectiveSpectrum.comap f hf
  c := { app U := CommRingCat.ofHom <| StructureSheaf.comap f hf _ _ Set.Subset.rfl }

@[simp] lemma germ_map_sectionInBasicOpen {p : ProjectiveSpectrum ğ’œâ‚‚}
    (c : NumDenSameDeg ğ’œâ‚ (p.comap f hf).1.toIdeal.primeCompl) :
    (toSheafedSpace ğ’œâ‚‚).presheaf.germ
      ((Opens.map (sheafedSpaceMap f hf).base).obj _) p (mem_basicOpen_den _ _ _)
      ((sheafedSpaceMap f hf).c.app _ (sectionInBasicOpen ğ’œâ‚ _ c)) =
    (toSheafedSpace ğ’œâ‚‚).presheaf.germ
      (ProjectiveSpectrum.basicOpen _ (f c.den)) p c.4
      (sectionInBasicOpen ğ’œâ‚‚ p (c.map _ le_rfl)) :=
  rfl

@[simp] lemma val_sectionInBasicOpen_apply (p : ProjectiveSpectrum.top ğ’œâ‚)
    (c : NumDenSameDeg ğ’œâ‚ p.1.toIdeal.primeCompl)
    (q : ProjectiveSpectrum.basicOpen ğ’œâ‚ c.den) :
    ((sectionInBasicOpen ğ’œâ‚ p c).val q).val = .mk c.num âŸ¨c.den, q.2âŸ© :=
  rfl

@[elementwise] theorem localRingHom_comp_stalkIso (p : ProjectiveSpectrum ğ’œâ‚‚) :
    (stalkIso ğ’œâ‚ (ProjectiveSpectrum.comap f hf p)).hom â‰«
      CommRingCat.ofHom (localRingHom f _ _ rfl) â‰«
        (stalkIso ğ’œâ‚‚ p).inv =
      (sheafedSpaceMap f hf).stalkMap p := by
  rw [â† Iso.eq_inv_comp, Iso.comp_inv_eq]
  ext : 1
  simp only [CommRingCat.hom_ofHom, stalkIso, RingEquiv.toCommRingCatIso_inv,
    RingEquiv.toCommRingCatIso_hom, CommRingCat.hom_comp]
  ext x : 2
  obtain âŸ¨c, rflâŸ© := x.mk_surjective
  simp only [val_localRingHom, val_mk, RingHom.comp_apply, RingHom.coe_coe]
  -- I sincerely apologise for your eyes.
  erw [stalkIso'_symm_mk]
  erw [PresheafedSpace.stalkMap_germ_apply]
  erw [germ_map_sectionInBasicOpen]
  erw [stalkIso'_germ]
  simp

noncomputable def map : Proj ğ’œâ‚‚ âŸ¶ Proj ğ’œâ‚ where
  __ := sheafedSpaceMap f hf
  prop p := .mk fun x hx â†¦ by
    rw [â† localRingHom_comp_stalkIso] at hx
    simp only [CommRingCat.hom_comp, CommRingCat.hom_ofHom, RingHom.coe_comp,
      Function.comp_apply] at hx
    have : IsLocalHom (stalkIso ğ’œâ‚‚ p).inv.hom := isLocalHom_of_isIso _
    replace hx := (isUnit_map_iff _ _).mp hx
    replace hx := IsLocalHom.map_nonunit _ hx
    have : IsLocalHom (stalkIso ğ’œâ‚ (p.comap f hf)).hom.hom := isLocalHom_of_isIso _
    exact (isUnit_map_iff _ _).mp hx

@[simp] theorem map_preimage_basicOpen (s : Aâ‚) :
    map f hf â»Â¹áµ basicOpen ğ’œâ‚ s = basicOpen ğ’œâ‚‚ (f s) :=
  rfl

theorem Î¹_comp_map (s : Aâ‚) :
    (basicOpen ğ’œâ‚‚ (f s)).Î¹ â‰« map f hf =
    (map f hf).resLE _ _ le_rfl â‰« (basicOpen ğ’œâ‚ s).Î¹ := by
  simp

/-- Given `f, g : X âŸ¶ Spec(R)`, if the two induced maps `R âŸ¶ Î“(X)` are equal, then `f = g`. -/
lemma _root_.AlgebraicGeometry.ext_to_Spec {X : Scheme} {R : Type*} [CommRing R]
    {f g : X âŸ¶ Spec(R)}
    (h : (Scheme.Î“SpecIso (.of R)).inv â‰« Scheme.Î“.map f.op =
      (Scheme.Î“SpecIso (.of R)).inv â‰« Scheme.Î“.map g.op) :
    f = g :=
  (Î“Spec.adjunction.homEquiv X (op <| .of R)).symm.injective <| unop_injective h

lemma _root_.AlgebraicGeometry.Î“_map_Spec_map_Î“SpecIso_inv
    {R S : CommRingCat.{u}} (f : R âŸ¶ S) (x : R) :
    Scheme.Î“.map (Spec.map f).op ((Scheme.Î“SpecIso R).inv x) = (Scheme.Î“SpecIso S).inv (f x) :=
  congr($((Scheme.Î“SpecIso_inv_naturality f).symm) x)

@[simp] lemma _root_.AlgebraicGeometry.Scheme.resLE_app_top
    {X Y : Scheme.{u}} (f : X âŸ¶ Y) (U : X.Opens) (V : Y.Opens) {h} :
    (f.resLE V U h).app âŠ¤ =
    V.topIso.hom â‰« f.appLE V U h â‰« U.topIso.inv := by
  simp [Scheme.Hom.resLE]

@[simp] lemma awayToSection_comp_appLE {i : â„•} {s : Aâ‚} (hs : s âˆˆ ğ’œâ‚ i) :
    awayToSection ğ’œâ‚ s â‰«
      Scheme.Hom.appLE (map f hf) (basicOpen ğ’œâ‚ s) (basicOpen ğ’œâ‚‚ (f s)) (by rfl) =
    CommRingCat.ofHom (Away.mapâ‚ f f.2 rfl : Away ğ’œâ‚ s â†’â‚[R] Away ğ’œâ‚‚ (f s)) â‰«
      awayToSection ğ’œâ‚‚ (f s) := by
  ext x
  obtain âŸ¨n, x, rflâŸ© := x.lof_surjective _ hs
  simp only [CommRingCat.hom_comp, smul_eq_mul, RingHom.coe_comp, Function.comp_apply,
    CommRingCat.hom_ofHom, RingHom.coe_coe]
  conv => enter[2,2]; exact Away.mapâ‚_lof _ _ _ _ _ _
  refine Subtype.ext <| funext fun p â†¦ ?_
  change HomogeneousLocalization.mk _ = .mk _
  ext
  simp

/--
The following square commutes:
```
Proj ğ’œâ‚‚         âŸ¶ Proj ğ’œâ‚
    ^                   ^
    |                   |
Spec Aâ‚‚[f(s)â»Â¹]â‚€ âŸ¶ Spec Aâ‚[sâ»Â¹]â‚€
```
-/
theorem awayÎ¹_comp_map {i : â„•} (hi : 0 < i) (s : Aâ‚) (hs : s âˆˆ ğ’œâ‚ i) :
    awayÎ¹ ğ’œâ‚‚ (f s) (f.2 hs) hi â‰« map f hf =
    Spec.map (CommRingCat.ofHom (Away.mapâ‚ f f.2 (by rfl)).toRingHom) â‰« awayÎ¹ ğ’œâ‚ s hs hi := by
  rw [awayÎ¹, awayÎ¹, Category.assoc, Î¹_comp_map, â† Category.assoc, â† Category.assoc]
  congr 1
  rw [Iso.inv_comp_eq, â† Category.assoc, Iso.eq_comp_inv]
  refine ext_to_Spec <| (cancel_mono (basicOpen ğ’œâ‚‚ (f s)).topIso.hom).mp ?_
  simp [basicOpenIsoSpec_hom, basicOpenToSpec_app_top, awayToSection_comp_appLE _ _ hs]

end AlgebraicGeometry.Proj
