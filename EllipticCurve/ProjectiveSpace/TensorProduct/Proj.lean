/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import EllipticCurve.ProjectiveSpace.TensorProduct.GradedAlgebra
import EllipticCurve.ProjectiveSpace.TensorProduct.ProjMap
import Mathlib.AlgebraicGeometry.ProjectiveSpectrum.Basic
import Mathlib.AlgebraicGeometry.Pullbacks
import Mathlib.LinearAlgebra.TensorProduct.Finiteness
import Mathlib.RingTheory.GradedAlgebra.Basic

/-! # Proj of Tensor Product

In this file we show `Proj (S âŠ—[R] ğ’œ) â‰… Spec S Ã—_R Proj ğ’œ` where `ğ’œ` is a graded `R`-algebra.
-/

universe u

open TensorProduct in
def AlgHom.liftBaseChange {R S A B : Type*}
    [CommSemiring R] [CommSemiring S] [Semiring A] [Semiring B]
    [Algebra R S] [Algebra R A] [Algebra R B] [Algebra S B] [IsScalarTower R S B]
    (f : A â†’â‚[R] B) :
    S âŠ—[R] A â†’â‚[S] B :=
  .ofLinearMap (.liftBaseChange S f) (by simp [Algebra.TensorProduct.one_def]) fun x y â†¦ by
    induction x using TensorProduct.induction_on with
    | zero => simp
    | add xâ‚ xâ‚‚ hxâ‚ hxâ‚‚ => simp [add_mul, hxâ‚, hxâ‚‚]
    | tmul sâ‚ aâ‚ => induction y using TensorProduct.induction_on with
      | zero => simp
      | add yâ‚ yâ‚‚ hyâ‚ hyâ‚‚ => simp [mul_add, hyâ‚, hyâ‚‚]
      | tmul sâ‚‚ aâ‚‚ => simp [Algebra.TensorProduct.tmul_mul_tmul, mul_smul, smul_comm sâ‚]

@[simp] lemma AlgHom.liftBaseChange_tmul {R S A B : Type*}
    [CommSemiring R] [CommSemiring S] [Semiring A] [Semiring B]
    [Algebra R S] [Algebra R A] [Algebra R B] [Algebra S B] [IsScalarTower R S B]
    (f : A â†’â‚[R] B) (s : S) (a : A) :
    f.liftBaseChange (s âŠ—â‚œ a) = s â€¢ f a := rfl

open TensorProduct in
@[ext high] theorem Algebra.TensorProduct.ext_ring {R S A B : Type*}
    [CommSemiring R] [Semiring A] [Algebra R A] [Semiring B] [Algebra R B]
    [CommSemiring S] [Algebra R S] [Algebra S B] [IsScalarTower R S B]
    {f g : S âŠ—[R] A â†’â‚[S] B}
    (h : (AlgHom.restrictScalars R f).comp Algebra.TensorProduct.includeRight =
      (AlgHom.restrictScalars R g).comp Algebra.TensorProduct.includeRight) :
    f = g :=
  ext (Subsingleton.elim _ _) h

section degree

noncomputable def DirectSum.degree {Î¹ M Ïƒ : Type*} [Zero M] [SetLike Ïƒ M] [ZeroMemClass Ïƒ M]
    [Zero Î¹] (â„³ : Î¹ â†’ Ïƒ) (x : M) : Î¹ :=
  open Classical in if h : x â‰  0 âˆ§ âˆƒ i, x âˆˆ â„³ i then h.2.choose else 0

namespace DirectSum

variable {Î¹ M Ïƒ : Type*} [SetLike Ïƒ M] (â„³ : Î¹ â†’ Ïƒ)

theorem degree_of_mem [AddCommMonoid M] [DecidableEq Î¹] [Zero Î¹] [AddSubmonoidClass Ïƒ M]
    [Decomposition â„³] (x : M) (i : Î¹) (hxâ‚€ : x â‰  0) (hxi : x âˆˆ â„³ i) : degree â„³ x = i := by
  rw [degree, dif_pos âŸ¨hxâ‚€, _, hxiâŸ©]
  generalize_proofs h
  exact degree_eq_of_mem_mem _ h.choose_spec hxi hxâ‚€

theorem mem_degree [AddCommMonoid M] [DecidableEq Î¹] [Zero Î¹] [AddSubmonoidClass Ïƒ M]
    [Decomposition â„³] (x : M) (hx : SetLike.IsHomogeneousElem â„³ x) : x âˆˆ â„³ (degree â„³ x) := by
  by_cases hxâ‚€ : x = 0
  Â· rw [hxâ‚€]; exact zero_mem _
  obtain âŸ¨i, hxiâŸ© := hx
  rwa [degree_of_mem â„³ x i hxâ‚€ hxi]

theorem decompose_of_homogeneous [AddCommMonoid M] [DecidableEq Î¹] [Zero Î¹] [AddSubmonoidClass Ïƒ M]
    [Decomposition â„³] (x : M) (hx : SetLike.IsHomogeneousElem â„³ x) :
    decompose â„³ x = of (fun i â†¦ â„³ i) (degree â„³ x) (âŸ¨x, mem_degree â„³ x hxâŸ© : â„³ _) :=
  decompose_of_mem â„³ _

theorem degree_mul [Semiring M] [AddSubmonoidClass Ïƒ M] [DecidableEq Î¹] [AddMonoid Î¹]
    [GradedRing â„³] (x y : M) (hx : SetLike.IsHomogeneousElem â„³ x)
    (hy : SetLike.IsHomogeneousElem â„³ y) (hxy : x * y â‰  0) :
    degree â„³ (x * y) = degree â„³ x + degree â„³ y :=
  degree_of_mem _ _ _ hxy <| SetLike.mul_mem_graded (mem_degree _ _ hx) (mem_degree _ _ hy)

theorem coe_apply_congr [AddCommMonoid M] [AddSubmonoidClass Ïƒ M]
    {x : â¨ i, â„³ i} {i j : Î¹} (h : i = j) : (x i : M) = x j := by
  subst h; rfl

end DirectSum

end degree

open DirectSum in
noncomputable def HomogeneousLocalization.projâ‚€ {R A : Type*}
    [CommRing R] [CommRing A] [Algebra R A] {Î¹ : Type*} [DecidableEq Î¹] [AddCancelCommMonoid Î¹]
    (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    (S : Submonoid A) (homog : S â‰¤ SetLike.homogeneousSubmonoid ğ’œ) :
    Localization S â†’â‚—[HomogeneousLocalization ğ’œ S] HomogeneousLocalization ğ’œ S where
  toFun x := x.liftOn (fun a s â†¦ .mk âŸ¨degree ğ’œ s.1, decompose ğ’œ a _,
    âŸ¨s, mem_degree _ _ (homog s.2)âŸ©, s.2âŸ©) fun {aâ‚ aâ‚‚} {sâ‚ sâ‚‚} h â†¦ by
      ext
      simp_rw [val_mk, Localization.mk_eq_mk_iff]
      rw [Localization.r_iff_exists] at h âŠ¢
      obtain âŸ¨s, hsâŸ© := h
      refine âŸ¨s, ?_âŸ©
      have hs' := congr((decompose ğ’œ $hs (degree ğ’œ (s : A) +
        (degree ğ’œ (sâ‚ : A) + degree ğ’œ (sâ‚‚ : A))) : A))
      simp_rw [decompose_mul, decompose_of_homogeneous _ _ (homog s.2),
        decompose_of_homogeneous _ _ (homog sâ‚.2), decompose_of_homogeneous _ _ (homog sâ‚‚.2),
        coe_of_mul_apply_add, coe_apply_congr _ (add_comm (degree ğ’œ (sâ‚ : A)) _),
        coe_of_mul_apply_add] at hs'
      exact hs'
  map_add' x y := Localization.induction_onâ‚‚ x y fun c d â†¦ by
    ext
    by_cases hsâ‚€ : 0 âˆˆ S
    Â· subsingleton [IsLocalization.uniqueOfZeroMem hsâ‚€]
    have ne_zero {x} (hx : x âˆˆ S) : (x : A) â‰  0 := fun hxâ‚€ â†¦ hsâ‚€ <| hxâ‚€ â–¸ hx
    simp_rw [val_add, Localization.add_mk, Localization.liftOn_mk, val_mk,
      Localization.add_mk, decompose_add, add_apply, Submonoid.coe_mul, decompose_mul,
      Submodule.coe_add, Subtype.coe_eta]
    rw [degree_mul _ _ _ (homog c.2.2) (homog d.2.2) (ne_zero (c.2 * d.2).2),
      decompose_of_homogeneous _ _ (homog c.2.2),
      decompose_of_homogeneous _ _ (homog d.2.2),
      coe_of_mul_apply_add, coe_apply_congr _ (add_comm (degree ğ’œ (c.2 : A)) _),
      coe_of_mul_apply_add]
    rfl
  map_smul' r x := Localization.induction_on x fun d â†¦ by
    obtain âŸ¨c, rflâŸ© := mk_surjective r
    ext
    by_cases hsâ‚€ : 0 âˆˆ S
    Â· subsingleton [IsLocalization.uniqueOfZeroMem hsâ‚€]
    have ne_zero {x} (hx : x âˆˆ S) : (x : A) â‰  0 := fun hxâ‚€ â†¦ hsâ‚€ <| hxâ‚€ â–¸ hx
    rw [RingHom.id_apply, Algebra.smul_def, smul_eq_mul, val_mul, algebraMap_apply, val_mk]
    simp_rw [Localization.mk_mul, Localization.liftOn_mk, val_mk, Localization.mk_mul,
      decompose_mul, decompose_coe, Subtype.coe_eta, Submonoid.coe_mul]
    rw [degree_mul _ _ _ âŸ¨_, c.den.2âŸ© (homog d.2.2) (ne_zero <| S.mul_mem c.den_mem d.2.2),
      degree_of_mem _ _ _ (ne_zero c.den_mem) c.den.2,
      coe_of_mul_apply_add]

theorem HomogeneousLocalization.projâ‚€_mk {R A : Type*}
    [CommRing R] [CommRing A] [Algebra R A] {Î¹ : Type*} [DecidableEq Î¹] [AddCancelCommMonoid Î¹]
    (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    (S : Submonoid A) (homog : S â‰¤ SetLike.homogeneousSubmonoid ğ’œ)
    (a : A) (s : S) :
    HomogeneousLocalization.projâ‚€ ğ’œ S homog (Localization.mk a s) =
    HomogeneousLocalization.mk âŸ¨DirectSum.degree ğ’œ (s : A), DirectSum.decompose ğ’œ a _,
      âŸ¨s, DirectSum.mem_degree _ _ (homog s.2)âŸ©, s.2âŸ© := rfl

@[simp] lemma HomogeneousLocalization.projâ‚€_val {R A : Type*}
    [CommRing R] [CommRing A] [Algebra R A] {Î¹ : Type*} [DecidableEq Î¹] [AddCancelCommMonoid Î¹]
    (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    (S : Submonoid A) (homog : S â‰¤ SetLike.homogeneousSubmonoid ğ’œ)
    (x : HomogeneousLocalization ğ’œ S) :
    HomogeneousLocalization.projâ‚€ ğ’œ S homog x.val = x := by
  ext
  by_cases hsâ‚€ : 0 âˆˆ S
  Â· subsingleton [IsLocalization.uniqueOfZeroMem hsâ‚€]
  induction x using Quotient.inductionOn' with
  | h c =>
    simp_rw [val, Quotient.liftOn'_mk'', NumDenSameDeg.embedding, projâ‚€_mk, mk,
      Quotient.liftOn'_mk'', NumDenSameDeg.embedding]
    rw [DirectSum.decompose_of_mem _ c.num.2, DirectSum.coe_of_apply, if_pos]
    rw [DirectSum.degree_of_mem _ _ _ (mt (Â· â–¸ c.den_mem) hsâ‚€) c.den.2]

noncomputable def HomogeneousLocalization.Away.projâ‚€ {R A : Type*}
    [CommRing R] [CommRing A] [Algebra R A] {Î¹ : Type*} [DecidableEq Î¹] [AddCancelCommMonoid Î¹]
    (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    {i : Î¹} {f : A} (hf : f âˆˆ ğ’œ i) :
    Localization.Away (f : A) â†’â‚—[HomogeneousLocalization.Away ğ’œ f]
      HomogeneousLocalization.Away ğ’œ f :=
  HomogeneousLocalization.projâ‚€ _ _ <| Submonoid.powers_le.mpr âŸ¨_, hfâŸ©

theorem HomogeneousLocalization.Away.projâ‚€_mk {R A : Type*}
    [CommRing R] [CommRing A] [Algebra R A] {Î¹ : Type*} [DecidableEq Î¹] [AddCancelCommMonoid Î¹]
    (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    {i : Î¹} {f : A} (hf : f âˆˆ ğ’œ i) (n : â„•) (a : A) (ha : a âˆˆ ğ’œ (n â€¢ i)) :
    projâ‚€ ğ’œ hf (.mk a âŸ¨f ^ n, n, rflâŸ©) = .mk _ hf n a ha :=
  projâ‚€_val _ _ _ (Away.mk _ hf _ _ _)

theorem HomogeneousLocalization.Away.projâ‚€_mk' {R A : Type*}
    [CommRing R] [CommRing A] [Algebra R A] {Î¹ : Type*} [DecidableEq Î¹] [AddCancelCommMonoid Î¹]
    (ğ’œ : Î¹ â†’ Submodule R A) [GradedAlgebra ğ’œ]
    {i : Î¹} {f : A} (hf : f âˆˆ ğ’œ i) (n : â„•) (a : ğ’œ (n â€¢ i)) :
    projâ‚€ ğ’œ hf (.mk a âŸ¨f ^ n, n, rflâŸ©) = .lof _ hf n a :=
  projâ‚€_mk _ _ _ _ _

open TensorProduct in
/-- `(S âŠ—[R] A)[(1 âŠ—â‚œ W)â»Â¹] â‰… (S âŠ—[R] A)[Wâ»Â¹]`. -/
noncomputable def IsLocalization.tensorEquiv (R S A Aâ‚ SAâ‚ : Type*)
    [CommSemiring R] [CommSemiring S] [CommSemiring A] [CommSemiring Aâ‚] [CommSemiring SAâ‚]
    [Algebra R S] [Algebra R A] (Wâ‚ : Submonoid A) (Wâ‚‚ : Submonoid (S âŠ—[R] A))
    (hw : Wâ‚.map Algebra.TensorProduct.includeRight = Wâ‚‚)
    [Algebra A Aâ‚] [IsLocalization Wâ‚ Aâ‚]
    [Algebra R Aâ‚] [IsScalarTower R A Aâ‚]
    [Algebra (S âŠ—[R] A) SAâ‚] [IsLocalization Wâ‚‚ SAâ‚]
    [Algebra R SAâ‚] [Algebra S SAâ‚] [IsScalarTower R S SAâ‚] [IsScalarTower S (S âŠ—[R] A) SAâ‚]
    [IsScalarTower R (S âŠ—[R] A) SAâ‚] :
    SAâ‚ â‰ƒâ‚[S] S âŠ—[R] Aâ‚ :=
  .ofAlgHom
  (IsLocalization.liftAlgHom
    (M := Wâ‚‚)
    (f := Algebra.TensorProduct.map (1 : S â†’â‚[S] S) (Algebra.algHom R A Aâ‚)) <| by
      rw [â† hw]
      rintro âŸ¨_, w, hw, rflâŸ©
      exact (IsLocalization.map_units _ âŸ¨w, hwâŸ©).map Algebra.TensorProduct.includeRight)
  (AlgHom.liftBaseChange <| IsLocalization.liftAlgHom (M := Wâ‚)
    (f := (Algebra.algHom _ _ _).comp (Algebra.TensorProduct.includeRight (R := R) (A := S)))
    fun w â†¦ IsLocalization.map_units (M := Wâ‚‚) SAâ‚ âŸ¨_, hw â–¸ âŸ¨_, w.2, rflâŸ©âŸ©)
  (Algebra.TensorProduct.ext_ring <| IsLocalization.algHom_ext Wâ‚ <| by ext; simp [Algebra.algHom])
  (IsLocalization.algHom_ext Wâ‚‚ <| by ext; simp [Algebra.algHom])

open TensorProduct in
/-- `(S âŠ—[R] A)[(1 âŠ—â‚œ W)â»Â¹] â‰… S âŠ—[R] A[Wâ»Â¹]`. -/
noncomputable def Localization.tensorEquiv (R S : Type*) {A : Type*}
    [CommSemiring R] [CommSemiring S] [CommSemiring A]
    [Algebra R S] [Algebra R A] (W : Submonoid A) :
    Localization (W.map (Algebra.TensorProduct.includeRight (R := R) (A := S))) â‰ƒâ‚[S]
    S âŠ—[R] Localization W :=
  IsLocalization.tensorEquiv R S A _ _ W _ rfl

open TensorProduct in
/-- `(S âŠ—[R] A)[(1 âŠ—â‚œ f)â»Â¹] â‰… S âŠ—[R] A[fâ»Â¹]`. -/
noncomputable def Localization.Away.tensorEquiv (R S : Type*) {A : Type*}
    [CommSemiring R] [CommSemiring S] [CommSemiring A]
    [Algebra R S] [Algebra R A] (f : A) :
    Away (1 âŠ—â‚œ[R] f : S âŠ—[R] A) â‰ƒâ‚[S] S âŠ—[R] Away f :=
  IsLocalization.tensorEquiv R S A _ _ (.powers f) (.powers (1 âŠ—â‚œ f)) (by simp)

@[simp] lemma Localization.Away.tensorEquiv_mk {R S : Type*} {A : Type*}
    [CommSemiring R] [CommSemiring S] [CommSemiring A]
    [Algebra R S] [Algebra R A] (f : A) (s : S) (a : A) (n : â„•) :
    tensorEquiv R S f (.mk (s âŠ—â‚œ a) âŸ¨1 âŠ—â‚œ (f ^ n), n, by simpâŸ©) = s âŠ—â‚œ .mk a âŸ¨f ^ n, n, rflâŸ© := by
  simp_rw [tensorEquiv, IsLocalization.tensorEquiv, AlgEquiv.ofAlgHom_apply,
    IsLocalization.liftAlgHom_apply, mk_eq_mk', IsLocalization.lift_mk',
    Units.mul_inv_eq_iff_eq_mul, IsUnit.coe_liftRight]
  simp only [Algebra.algHom, AlgHom.toRingHom_eq_coe, RingHom.coe_coe,
    Algebra.TensorProduct.map_tmul, AlgHom.one_apply, AlgHom.coe_mk, â† mk_one_eq_algebraMap,
    â† mk_eq_mk', RingHom.toMonoidHom_eq_coe, AlgHom.toRingHom_toMonoidHom,
    MonoidHom.restrict_apply, MonoidHom.coe_coe, Algebra.TensorProduct.tmul_mul_tmul, mul_one,
    mk_mul]
  congr 1
  exact mk_eq_mk_iff.mpr <| r_iff_exists.mpr âŸ¨1, by simp [mul_comm]âŸ©

variable {R A : Type u} [CommRing R] [CommRing A] [Algebra R A]
  (ğ’œ : â„• â†’ Submodule R A) [GradedAlgebra ğ’œ]
  (S : Type u) [CommRing S] [Algebra R S]

namespace AlgebraicGeometry

open TensorProduct CategoryTheory Limits CommRingCat

noncomputable def Proj.toSpec : Proj ğ’œ âŸ¶ Spec(R) :=
  Proj.toSpecZero ğ’œ â‰« Spec.map (ofHom (algebraMap R (ğ’œ 0)))

lemma baseChange_iSupEqTop :
    (HomogeneousIdeal.irrelevant fun n â†¦ (ğ’œ n).baseChange S).toIdeal â‰¤
    Ideal.span (Set.range fun f : (Proj.affineOpenCover ğ’œ).Iâ‚€ â†¦ 1 âŠ—â‚œ[R] f.2) := by
  intro x hx
  classical
  rw [â† DirectSum.sum_support_decompose (fun n â†¦ (ğ’œ n).baseChange S) x]
  refine sum_mem fun i hi â†¦ ?_
  have hiâ‚€ : i â‰  0 := fun hiâ‚€ â†¦ DFinsupp.mem_support_iff.mp hi (hiâ‚€ â–¸ by simpa using hx)
  generalize DirectSum.decompose (fun n â†¦ (ğ’œ n).baseChange S) x i = y
  obtain âŸ¨_, y, rflâŸ© := y
  obtain âŸ¨s, rflâŸ© := exists_finset y
  simp only [map_sum, LinearMap.baseChange_tmul, Submodule.subtype_apply]
  refine Ideal.sum_mem _ fun c hc â†¦ ?_
  rw [â† mul_one c.1, â† one_mul (c.2: A), â† Algebra.TensorProduct.tmul_mul_tmul]
  refine Ideal.mul_mem_left _ _ <| Ideal.subset_span âŸ¨âŸ¨âŸ¨i, pos_of_ne_zero hiâ‚€âŸ©, _âŸ©, rflâŸ©

noncomputable def Proj.openCoverBaseChange :
    (Proj fun n â†¦ (ğ’œ n).baseChange S).AffineOpenCover :=
  Proj.openCoverOfISupEqTop _ (fun f : (Proj.affineOpenCover ğ’œ).Iâ‚€ â†¦ 1 âŠ—â‚œ f.2)
    (fun f â†¦ Submodule.tmul_mem_baseChange_of_mem _ f.2.2) (fun f â†¦ f.1.2)
    (baseChange_iSupEqTop ğ’œ S)

set_option maxHeartbeats 999999 in
-- I don't know why
noncomputable def awayBaseChange {i : â„•} {f : A} (hf : f âˆˆ ğ’œ i) :
    HomogeneousLocalization.Away (fun n â†¦ (ğ’œ n).baseChange S) (1 âŠ—â‚œ[R] f) â‰ƒâ‚[S]
      S âŠ—[R] HomogeneousLocalization.Away ğ’œ f := by
  let fâ‚ : HomogeneousLocalization.Away (fun n â†¦ (ğ’œ n).baseChange S) (1 âŠ—â‚œ[R] f) â†’â‚[S]
      Localization.Away (1 âŠ—â‚œ f : S âŠ—[R] A) := Algebra.algHom _ _ _
  let fâ‚‚ : Localization.Away (1 âŠ—â‚œ f : S âŠ—[R] A) â‰ƒâ‚[S]
      S âŠ—[R] Localization.Away (f : A) := Localization.Away.tensorEquiv _ _ _
  let fâ‚ƒ : S âŠ—[R] Localization.Away (f : A) â†’â‚—[S] S âŠ—[R] HomogeneousLocalization.Away ğ’œ f :=
    ((HomogeneousLocalization.Away.projâ‚€ ğ’œ hf).restrictScalars R).baseChange S
  let forwards : HomogeneousLocalization.Away (fun n â†¦ (ğ’œ n).baseChange S) (1 âŠ—â‚œ[R] f) â†’â‚—[S]
      S âŠ—[R] HomogeneousLocalization.Away ğ’œ f :=
    fâ‚ƒ âˆ˜â‚— fâ‚‚.toLinearMap âˆ˜â‚— fâ‚.toLinearMap
  let backwards : S âŠ—[R] HomogeneousLocalization.Away ğ’œ f â†’â‚[S]
      HomogeneousLocalization.Away (fun n â†¦ (ğ’œ n).baseChange S) (1 âŠ—â‚œ[R] f) :=
    AlgHom.liftBaseChange <| HomogeneousLocalization.Away.mapâ‚
      (Algebra.TensorProduct.includeRight (R := R) (A := S))
      (fun _ _ â†¦ Submodule.tmul_mem_baseChange_of_mem _) rfl
  refine
    have left : backwards.toLinearMap âˆ˜â‚— forwards = 1 := ?_
    have right : forwards âˆ˜â‚— backwards.toLinearMap = 1 := ?_
    .symm { __ := backwards, invFun := forwards, left_inv := ?_, right_inv := ?_ }
  Â· ext x
    obtain âŸ¨n, a, rflâŸ© := x.lof_surjective _ (Submodule.tmul_mem_baseChange_of_mem _ hf)
    obtain âŸ¨a, rflâŸ© := Submodule.toBaseChange_surjective _ _ a
    simp only [smul_eq_mul, LinearMap.coe_comp, Function.comp_apply, AlgHom.toLinearMap_apply,
      Module.End.one_apply]
    induction a using TensorProduct.induction_on with
    | zero => simp
    | add aâ‚ aâ‚‚ haâ‚ haâ‚‚ => simp [haâ‚, haâ‚‚]
    | tmul s a =>
      simp only [forwards, fâ‚, fâ‚‚, fâ‚ƒ, backwards, Algebra.algHom]
      simp only [smul_eq_mul, LinearMap.coe_comp, Function.comp_apply, AlgHom.toLinearMap_apply,
        AlgHom.coe_mk, HomogeneousLocalization.algebraMap_apply, AlgEquiv.toLinearMap_apply]
      erw [HomogeneousLocalization.Away.val_lof]
      simp only [smul_eq_mul, Submodule.toBaseChange_tmul_coe, Algebra.TensorProduct.tmul_pow,
        one_pow, Localization.Away.tensorEquiv_mk, LinearMap.baseChange_tmul,
        LinearMap.coe_restrictScalars, HomogeneousLocalization.Away.projâ‚€_mk',
        AlgHom.liftBaseChange_tmul, HomogeneousLocalization.val_smul]
      erw [HomogeneousLocalization.Away.mapâ‚_lof]
      rw [HomogeneousLocalization.Away.val_lof]
      simp only [smul_eq_mul, Algebra.TensorProduct.includeRight_apply,
        Algebra.TensorProduct.tmul_pow, one_pow, Localization.smul_mk]
      congr 1
      rw [â† tmul_eq_smul_one_tmul]
  Â· ext x
    obtain âŸ¨n, a, rflâŸ© := x.lof_surjective _ hf
    simp only [forwards, fâ‚, fâ‚‚, fâ‚ƒ, backwards, Algebra.algHom]
    simp only [AlgebraTensorModule.curry_apply, LinearMap.restrictScalars_comp, smul_eq_mul,
      curry_apply, LinearMap.coe_comp, LinearMap.coe_restrictScalars, Function.comp_apply,
      AlgHom.toLinearMap_apply, AlgHom.liftBaseChange_tmul, one_smul, AlgHom.coe_mk,
      HomogeneousLocalization.algebraMap_apply, AlgEquiv.toLinearMap_apply, Module.End.one_apply]
    erw [HomogeneousLocalization.Away.mapâ‚_lof]
    rw [HomogeneousLocalization.Away.val_lof]
    simp [HomogeneousLocalization.Away.projâ‚€_mk']
  Â· exact fun x â†¦ congr($right x)
  Â· exact fun x â†¦ congr($left x)

@[simps!] def _root_.GradedAlgebra.toTensor : ğ’œ â†’áµáµƒ fun n â†¦ (ğ’œ n).baseChange S where
  __ := Algebra.TensorProduct.includeRight
  map_mem' _ _ := Submodule.tmul_mem_baseChange_of_mem _

lemma _root_.GradedAlgebra.toTensor_admissible :
    (HomogeneousIdeal.irrelevant fun n â†¦ (ğ’œ n).baseChange S) â‰¤
    (HomogeneousIdeal.irrelevant ğ’œ).map (GradedAlgebra.toTensor ğ’œ S) := by
  refine (HomogeneousIdeal.irrelevant_le _).mpr fun i hi x hx â†¦ ?_
  obtain âŸ¨a, haâŸ© := Submodule.toBaseChange_surjective _ _ âŸ¨x, hxâŸ©
  replace ha := congr(($ha).val); subst ha
  induction a with
  | zero => simp
  | add => simp [*, add_mem]
  | tmul s a =>
    simp only [Submodule.toBaseChange_tmul_coe]
    rw [tmul_eq_smul_one_tmul, â† algebraMap_smul (S âŠ—[R] A), smul_eq_mul]
    exact Ideal.mul_mem_left _ _ <| Ideal.mem_map_of_mem _ <|
      HomogeneousIdeal.mem_irrelevant_of_mem _ hi a.2

@[simp] lemma awayBaseChange_symm_tmul
    {i : â„•} {f : A} (hf : f âˆˆ ğ’œ i) {s : S} {x : HomogeneousLocalization.Away ğ’œ f} :
    (awayBaseChange ğ’œ S hf).symm (s âŠ—â‚œ x) =
    s â€¢ .map (GradedAlgebra.toTensor ğ’œ S) rfl x := by
  obtain âŸ¨n, a, rflâŸ© := x.lof_surjective _ hf
  rw [AlgEquiv.symm_apply_eq, HomogeneousLocalization.Away.map_lof, map_smul]
  simp only [smul_eq_mul, awayBaseChange, AlgHom.toRingHom_eq_coe, RingHom.toMonoidHom_eq_coe,
    AlgHom.toRingHom_toMonoidHom, OneHom.toFun_eq_coe, MonoidHom.toOneHom_coe, MonoidHom.coe_coe,
    Algebra.algHom, LinearMap.coe_comp, AlgEquiv.symm_mk, GradedAlgebra.toTensor_toFun,
    AlgEquiv.coe_mk, Equiv.coe_fn_mk, Function.comp_apply, AlgHom.toLinearMap_apply, AlgHom.coe_mk,
    HomogeneousLocalization.algebraMap_apply, AlgEquiv.toLinearMap_apply]
  conv => enter [2,2,2,2]; exact HomogeneousLocalization.Away.val_lof _ _ _ _
  simp [HomogeneousLocalization.Away.lof, HomogeneousLocalization.lof,
    HomogeneousLocalization.Away.projâ‚€_mk, HomogeneousLocalization.Away.mk,
    â† tmul_eq_smul_one_tmul]

@[simp] lemma awayBaseChange_lof {i : â„•} {f : A} (hf : f âˆˆ ğ’œ i) {s : S} {n : â„•} {a : ğ’œ (n â€¢ i)} :
    awayBaseChange ğ’œ S hf (.lof (fun n â†¦ (ğ’œ n).baseChange S)
      (Submodule.tmul_mem_baseChange_of_mem _ hf) n (Submodule.toBaseChange _ _ (s âŠ—â‚œ a))) =
    s âŠ—â‚œ .lof _ hf n a := by
  rw [â† AlgEquiv.eq_symm_apply, awayBaseChange_symm_tmul,
    HomogeneousLocalization.Away.map_lof, tmul_eq_smul_one_tmul s, map_smul, map_smul]
  rfl

noncomputable def Proj.baseChangeIsoComponent {i : â„•} {f : A} (hf : f âˆˆ ğ’œ i) :
    Spec(HomogeneousLocalization.Away (fun n â†¦ (ğ’œ n).baseChange S) (1 âŠ—â‚œ[R] f)) â‰…
    pullback (Spec.map (ofHom (algebraMap R S)))
      (Spec.map (ofHom (algebraMap R (HomogeneousLocalization.Away ğ’œ f)))) :=
  Scheme.Spec.mapIso (awayBaseChange ğ’œ S hf).toCommRingCatIso.op.symm â‰ªâ‰«
  (pullbackSpecIso _ _ _).symm

@[reassoc (attr := simp)] lemma Proj.baseChangeIsoComponent_hom_comp_pullback_fst
    {i : â„•} {f : A} (hf : f âˆˆ ğ’œ i) :
    (Proj.baseChangeIsoComponent ğ’œ S hf).hom â‰« pullback.fst _ _ =
    Spec.map (ofHom (algebraMap S _)) := by
  simp only [HomogeneousLocalization.algebraMap_eq', ofHom_comp, baseChangeIsoComponent,
    Scheme.Spec_obj, AlgEquiv.toRingEquiv_eq_coe, Functor.mapIso_symm, Iso.trans_hom, Iso.symm_hom,
    Functor.mapIso_inv, Iso.op_inv, RingEquiv.toCommRingCatIso_inv, Scheme.Spec_map,
    Quiver.Hom.unop_op, Category.assoc]
  conv => enter [1,2]; exact pullbackSpecIso_inv_fst ..
  simp only [â† Spec.map_comp, â† ofHom_comp]
  congr 2; ext s
  simp [â† AlgEquiv.symm_toRingEquiv, IsScalarTower.algebraMap_apply S (S âŠ—[R] A) (Localization _),
    â† Localization.mk_one_eq_algebraMap, tmul_eq_smul_one_tmul s, â† Localization.smul_mk,
    â† Algebra.TensorProduct.one_def, Localization.mk_one]

@[reassoc (attr := simp)] lemma Proj.baseChangeIsoComponent_hom_comp_pullback_snd
    {i : â„•} {f : A} (hf : f âˆˆ ğ’œ i) :
    (Proj.baseChangeIsoComponent ğ’œ S hf).hom â‰« pullback.snd _ _ =
    Spec.map (ofHom (HomogeneousLocalization.Away.map (GradedAlgebra.toTensor ..) rfl)) := by
  simp only [HomogeneousLocalization.algebraMap_eq', ofHom_comp, baseChangeIsoComponent,
    Scheme.Spec_obj, AlgEquiv.toRingEquiv_eq_coe, Functor.mapIso_symm, Iso.trans_hom, Iso.symm_hom,
    Functor.mapIso_inv, Iso.op_inv, RingEquiv.toCommRingCatIso_inv, Scheme.Spec_map,
    Quiver.Hom.unop_op, Category.assoc, GradedAlgebra.toTensor_toFun]
  conv => enter [1,2]; exact pullbackSpecIso_inv_snd ..
  rw [â† Spec.map_comp, â† ofHom_comp]
  congr 2; ext x
  simp [â† AlgEquiv.symm_toRingEquiv]

@[reassoc] lemma Proj.map_toSpec {R Râ‚ Râ‚‚ A B : Type u}
    [CommRing R] [CommRing Râ‚] [CommRing Râ‚‚] [CommRing A] [CommRing B]
    [Algebra R Râ‚] [Algebra R Râ‚‚] [Algebra R A] [Algebra R B]
    [Algebra Râ‚ A] [IsScalarTower R Râ‚ A] [Algebra Râ‚‚ B] [IsScalarTower R Râ‚‚ B]
    (ğ’œ : â„• â†’ Submodule Râ‚ A) [GradedAlgebra ğ’œ]
    (â„¬ : â„• â†’ Submodule Râ‚‚ B) [GradedAlgebra â„¬]
    (f : ğ’œ â†’áµáµƒ â„¬) (hf) (hfr : âˆ€ r, f (algebraMap R A r) = algebraMap R B r) :
    Proj.map f hf â‰« Proj.toSpec ğ’œ â‰« Spec.map (ofHom (algebraMap R Râ‚)) =
    Proj.toSpec â„¬ â‰« Spec.map (ofHom (algebraMap R Râ‚‚)) := by
  simp only [toSpec, Category.assoc, â† Spec.map_comp, â† ofHom_comp, map_comp_toSpecZero_assoc]
  congr 3; ext; simp [â† IsScalarTower.algebraMap_apply, hfr]

@[reassoc (attr := simp)] lemma Proj.map_toTensor_toSpec :
    Proj.map _ (GradedAlgebra.toTensor_admissible ğ’œ S) â‰« Proj.toSpec ğ’œ =
    Proj.toSpec _ â‰« Spec.map (ofHom (algebraMap R S)) := by
  simpa using Proj.map_toSpec (R := R) _ _ _ (GradedAlgebra.toTensor_admissible ğ’œ S) fun r â†¦ by
    simp [Algebra.TensorProduct.one_def, Algebra.algebraMap_eq_smul_one r, smul_tmul']

noncomputable def ofProjTensor :
    Proj (fun n â†¦ (ğ’œ n).baseChange S) âŸ¶
    pullback (Spec.map (ofHom (algebraMap R S))) (Proj.toSpec ğ’œ) :=
  pullback.lift (Proj.toSpec _) (Proj.map _ <| GradedAlgebra.toTensor_admissible _ _) <| by simp

@[reassoc (attr := simp)] lemma Proj.awayÎ¹_comp_toSpec
    {i : â„•} (hi : 0 < i) {s : A} (hs : s âˆˆ ğ’œ i) :
    Proj.awayÎ¹ ğ’œ s hs hi â‰« Proj.toSpec ğ’œ = Spec.map (ofHom (algebraMap _ _)) := by
  simp [toSpec, awayÎ¹_toSpecZero_assoc]

/--
The following square commutes:
```
Proj(S âŠ—[R] ğ’œ) ---------âŸ¶ Spec(S) Ã—[Spec(R)] Proj(ğ’œ)
    ^           ofProjTensor             ^
    |                                    |
    | awayÎ¹                              | ğŸ™ Ã— awayÎ¹
    |                                    |
    |           baseChangeIsoComponent   |
Spec((SâŠ—[R]A)[(1âŠ—s)â»Â¹]) âŸ¶ Spec(S) Ã—[Spec(R)] Spec(A[sâ»Â¹])
```
-/
lemma awayÎ¹_comp_ofProjTensor {i : â„•} (hi : 0 < i) {s : A} (hs : s âˆˆ ğ’œ i) :
    Proj.awayÎ¹ (fun n â†¦ (ğ’œ n).baseChange S) (1 âŠ—â‚œ s) (Submodule.tmul_mem_baseChange_of_mem _ hs)
      hi â‰« ofProjTensor ğ’œ S =
    (Proj.baseChangeIsoComponent ğ’œ S hs).hom â‰«
      pullback.map _ _ _ _ (ğŸ™ _) (Proj.awayÎ¹ _ s hs hi) (ğŸ™ _) (by simp) (by simp) :=
  pullback.hom_ext (by simp [- HomogeneousLocalization.algebraMap_eq', ofProjTensor]) <| by
  simpa [- HomogeneousLocalization.algebraMap_eq', ofProjTensor] using
    Proj.awayÎ¹_comp_map _ (GradedAlgebra.toTensor_admissible ğ’œ S) hi s hs

/- -- https://math.arizona.edu/~cais/CourseNotes/AlgGeom04/notes216.pdf
noncomputable def projTensorProduct : Proj (fun n â†¦ (ğ’œ n).baseChange S) â‰…
    pullback (Spec.map (ofHom (algebraMap R S))) (Proj.toSpec ğ’œ) where
  hom := (Proj.openCoverBaseChange ğ’œ S).openCover.glueMorphisms
    (fun f â†¦ (Proj.baseChangeIsoComponent _ _ f).hom â‰«
      (Scheme.Pullback.openCoverOfRight (Proj.affineOpenCover ğ’œ).openCover _ _).f f)
    (by simp)
  inv := (Scheme.Pullback.openCoverOfRight (Proj.affineOpenCover ğ’œ).openCover _ _).glueMorphisms
    (fun f â†¦ (Proj.baseChangeIsoComponent _ _ f).inv â‰«
      (Proj.openCoverBaseChange ğ’œ S).openCover.f f)
    _
  hom_inv_id := _
  inv_hom_id := _ -/

end AlgebraicGeometry
